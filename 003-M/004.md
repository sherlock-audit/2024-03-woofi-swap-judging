Jovial Olive Peacock

medium

# `crossSwap()` - Refunds from Stargate will be sent to the incorrect address which will lead to a loss of assets for the users

## Summary
Refunds from Stargate will be sent to the incorrect address (`WooCrossRouterForWidget.sol`) which will lead to a loss of assets for the users.

## Vulnerability Detail
The `WooCrossRouterForWidget.crossSwap()` uses `WooCrossChainRouterV4`'s `crossSwap()` function to execute cross-chain swaps: 
```solidity
    function crossSwap(address payable to, IWooCrossChainRouterV3.SrcInfos memory srcInfos, IWooCrossChainRouterV3.DstInfos memory dstInfos, IWooCrossChainRouterV3.Src1inch calldata src1inch, IWooCrossChainRouterV3.Dst1inch calldata dst1inch, FeeInfo calldata feeInfo) external payable whenNotPaused nonReentrant {
        //Checks
        .....
        crossRouter.crossSwap{value: msgValue}(refId, to, srcInfos, dstInfos, src1inch, dst1inch);
    }
```
The  `WooCrossChainRouterV4.crossSwap()` function use Stargate Bridge to bridge assets using `_bridgeByStargate()`:
```solidity
    function crossSwap(uint256 refId, address payable to, SrcInfos memory srcInfos, DstInfos calldata dstInfos, Src1inch calldata src1inch, Dst1inch calldata dst1inch) external payable whenNotPaused nonReentrant {
         // Checks
            // Step 1: transfer
            .....
            // Step 2: local swap by 1inch router
        .....

        // Step 3: deduct the swap fee
        .....

        // Step 4: cross chain swap by StargateRouter
        _bridgeByStargate(refId, to, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch);

        emit WooCrossSwapOnSrcChain(refId, _msgSender(), to, srcInfos.fromToken, srcInfos.fromAmount, srcInfos.bridgeToken, srcInfos.minBridgeAmount, bridgeAmount, src1inch.swapRouter == address(0) ? 0 : 1, fee);
    }
```
The `_bridgeByStargate()` calls Stargate's `Swap()` function and here is where the issue lies:
```solidity
function _bridgeByStargate(uint256 refId, address payable to, uint256 msgValue, uint256 bridgeAmount, SrcInfos memory srcInfos, DstInfos calldata dstInfos, Dst1inch calldata dst1inch) internal {
        .....
        stargateRouter.swap{value: msgValue}( ///@audit qouteLayerZeroFee not utilised?
            dstInfos.chainId, // dst chain id
            sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken), // bridge token's pool id on src chain
            sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken), // bridge token's pool id on dst chain
            ///@audit-issue refund addr is set to msgSender - WooCrossRouterForWidget calls this func
@>          payable(_msgSender()), // rebate address 
            bridgeAmount, // swap amount on src chain
            dstMinBridgeAmount, // min received amount on dst chain
            obj, // config: dstGasForCall, dstAirdropNativeAmount, dstReceiveAirdropNativeTokenAddr
            dstWooCrossChainRouter, // smart contract to call on dst chain
            payload // payload to piggyback
        );
    }
```
Notice that the `refundAddress` is specified as `_msgSender()`/`msg.Sender`, in this case msg.sender is the WooCrossRouterForWidget since that's the address that made the call to WooCrossChainRouterV4.

The `refundAddress` is used for refunding any excess native tokens, that are sent to Stargate/LZ in order to pay for the gas. The excess will be refunded on the source chain.

So, If a user sends 0.5 ETH for gas and Stargate only needs 0.1ETH, then 0.4ETH will be sent to the `refundAddress`.

### PoC
1. Alice wants to swap and bridge some tokens from Ethereum to Arbitrum.
2. For simplicity we'll assume that Stargate will need 0.1 ETH in order to pay for gas fees.
3. Alice sends 0.2 ETH, to make sure his swap gets executed smoothly
4. The flow of functions is executed and the extra 0.1 ETH is refunded from Stargate to `refundAddress`, which is set to `msg.sender`, which is `WooCrossRouterForWidget`.
5. Alice loses here 0.1 ETH, as there is no way for her withdraw her ETH from WooCrossRouterForWidget 

## Impact
- Users will not get Stargate Refunds.

## Code Snippet
- https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossRouterForWidget.sol#L133
- https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L260

## Tool used

[Secret Sauce](https://twitter.com/0x_Shaheen/status/1722664258142650806)

## Recommendation
Let Users specify a `refundAddress` when calling `crossSwap()`, Use the address that the user specifies instead of `msg.sender`:
```diff
    function crossSwap(  //WooCrossRouterForWidget.sol
        address payable to,
        IWooCrossChainRouterV3.SrcInfos memory srcInfos,
        IWooCrossChainRouterV3.DstInfos memory dstInfos,
        IWooCrossChainRouterV3.Src1inch calldata src1inch,
        IWooCrossChainRouterV3.Dst1inch calldata dst1inch,
        FeeInfo calldata feeInfo,
+     address refundAddr
    ) external payable whenNotPaused nonReentrant {
        require(srcInfos.fromToken != address(0), "WooCrossRouterForWidget: !srcInfos.fromToken");
        require(dstInfos.toToken != address(0), "WooCrossRouterForWidget: !dstInfos.toToken");
        require(to != address(0), "WooCrossRouterForWidget: !to");

        uint256 msgValue = msg.value;
        if (srcInfos.fromToken == ETH_PLACEHOLDER_ADDR) {
            require(msg.value >= srcInfos.fromAmount, "WooCrossRouterForWidget: !msg.value");
            uint256 fee = (srcInfos.fromAmount * feeInfo.feeRate) / FEE_BASE;
            TransferHelper.safeTransferETH(feeInfo.feeAddr, fee);
            msgValue -= fee;
            srcInfos.fromAmount -= fee;
        } else {
            TransferHelper.safeTransferFrom(srcInfos.fromToken, msg.sender, address(this), srcInfos.fromAmount);
            uint256 fee = (srcInfos.fromAmount * feeInfo.feeRate) / FEE_BASE;
            TransferHelper.safeTransfer(srcInfos.fromToken, feeInfo.feeAddr, fee);
            srcInfos.fromAmount -= fee;
            TransferHelper.safeApprove(srcInfos.fromToken, address(crossRouter), srcInfos.fromAmount);
        }

        uint256 refId = nonceCounter.increment(dstInfos.chainId);

-        crossRouter.crossSwap{value: msgValue}(refId, to, srcInfos, dstInfos, src1inch, dst1inch);
+        crossRouter.crossSwap{value: msgValue}(refId, to, srcInfos, dstInfos, src1inch, dst1inch, refundAddr);
    }
```
```diff
    function crossSwap(  //WooCrossChainRouterV4.sol
        uint256 refId,
        address payable to,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Src1inch calldata src1inch,
        Dst1inch calldata dst1inch,
+      address refundAddr
    ) external payable whenNotPaused nonReentrant {
        require(srcInfos.fromToken != address(0), "WooCrossChainRouterV3: !srcInfos.fromToken");
        require(
            dstInfos.toToken != address(0) && dstInfos.toToken != sgInfo.sgETHs(dstInfos.chainId),
            "WooCrossChainRouterV3: !dstInfos.toToken"
        );
        require(to != address(0), "WooCrossChainRouterV3: !to");

        uint256 msgValue = msg.value;
        uint256 bridgeAmount;
        uint256 fee = 0;

        {
            // Step 1: transfer
            if (srcInfos.fromToken == ETH_PLACEHOLDER_ADDR) {
                require(srcInfos.fromAmount <= msgValue, "WooCrossChainRouterV3: !srcInfos.fromAmount");
                srcInfos.fromToken = weth;
                IWETH(weth).deposit{value: srcInfos.fromAmount}();
                msgValue -= srcInfos.fromAmount;
            } else {
                TransferHelper.safeTransferFrom(srcInfos.fromToken, msg.sender, address(this), srcInfos.fromAmount);
            }

            // Step 2: local swap by 1inch router
            if (srcInfos.fromToken != srcInfos.bridgeToken) {
                TransferHelper.safeApprove(srcInfos.fromToken, address(wooRouter), srcInfos.fromAmount);
                if (src1inch.swapRouter != address(0)) {
                    // external swap via 1inch
                    bridgeAmount = wooRouter.externalSwap(
                        src1inch.swapRouter,
                        src1inch.swapRouter,
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        src1inch.data
                    );
                    fee = (bridgeAmount * srcExternalFeeRate) / FEE_BASE;
                } else {
                    // swap via WOOFi
                    bridgeAmount = wooRouter.swap(
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        to
                    );
                }
            } else {
                require(
                    srcInfos.fromAmount == srcInfos.minBridgeAmount,
                    "WooCrossChainRouterV3: !srcInfos.minBridgeAmount"
                );
                bridgeAmount = srcInfos.fromAmount;
            }

            require(
                bridgeAmount <= IERC20(srcInfos.bridgeToken).balanceOf(address(this)),
                "WooCrossChainRouterV3: !bridgeAmount"
            );
        }

        // Step 3: deduct the swap fee
        bridgeAmount -= fee;

        // Step 4: cross chain swap by StargateRouter
-        _bridgeByStargate(refId, to, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch);
+        _bridgeByStargate(refId, to, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch, refundAddr);

        emit WooCrossSwapOnSrcChain(
            refId,
            _msgSender(),
            to,
            srcInfos.fromToken,
            srcInfos.fromAmount,
            srcInfos.bridgeToken,
            srcInfos.minBridgeAmount,
            bridgeAmount,
            src1inch.swapRouter == address(0) ? 0 : 1,
            fee
        );
    }
```
```diff
    function _bridgeByStargate(
        uint256 refId,
        address payable to,
        uint256 msgValue,
        uint256 bridgeAmount,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Dst1inch calldata dst1inch,
+       address refundAddr
    ) internal {
        require(
            sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken) > 0,
            "WooCrossChainRouterV3: !srcInfos.bridgeToken"
        );
        require(
            sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken) > 0,
            "WooCrossChainRouterV3: !dstInfos.bridgeToken"
        );

        bytes memory payload = abi.encode(refId, to, dstInfos.toToken, dstInfos.minToAmount, dst1inch);

        uint256 dstMinBridgeAmount = (bridgeAmount * (10000 - bridgeSlippage)) / 10000;
        bytes memory dstWooCrossChainRouter = abi.encodePacked(wooCrossRouters[dstInfos.chainId]);

        IStargateRouter.lzTxObj memory obj = IStargateRouter.lzTxObj(
            dstInfos.dstGasForCall,
            dstInfos.airdropNativeAmount,
            abi.encodePacked(to)
        );
        IStargateRouter stargateRouter = IStargateRouter(sgInfo.sgRouter());

        if (srcInfos.bridgeToken == weth) {
            IWETH(weth).withdraw(bridgeAmount);
            msgValue += bridgeAmount;
        } else {
            TransferHelper.safeApprove(srcInfos.bridgeToken, sgInfo.sgRouter(), bridgeAmount);
        }

        stargateRouter.swap{value: msgValue}(
            dstInfos.chainId, // dst chain id
            sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken), // bridge token's pool id on src chain
            sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken), // bridge token's pool id on dst chain
            ///@audit-issue refund addr is set to msgSender - WooCrossRouterForWidget calls this func
-           payable(_msgSender()), // rebate address 
+           payable(refundAddr), // rebate address 
            bridgeAmount, // swap amount on src chain
            dstMinBridgeAmount, // min received amount on dst chain
            obj, // config: dstGasForCall, dstAirdropNativeAmount, dstReceiveAirdropNativeTokenAddr
            dstWooCrossChainRouter, // smart contract to call on dst chain
            payload // payload to piggyback
        );
    }
```
