Melodic Navy Goat

medium

# Stargate Pools convert rate leads to loss of tokens and dangling allowances

## Summary

The Stargate pools conversion rate leads to a loss of tokens inside the `WooCrossChainRouterV4` contract and dangling allowances to the StargateRouter contract.

## Vulnerability Detail
Stargate pools have a concept of convert rate. It's calculated based on the `sharedDecimals` and `localDecimals` for a specific pool. For example, the DAI Pool has the `sharedDecimals` set to 6 while `localDecimals` is 18.

The convert rate is then: `10^(localDecimals - sharedDecimals) = 10^12`.

Here is the [DAI Pool](https://etherscan.io/address/0x0Faf1d2d3CED330824de3B8200fc8dc6E397850d#readContract) on Ethereum and the convert rate logic inside the [Pool contract](https://github.com/stargate-protocol/stargate/blob/5f0dfd2/contracts/Pool.sol#L140).

As a result any user that is using the [`WooCrossChainRouterV4:crossSwap`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L66) function will be affected by this issue. 

Let's take the following example:

- User wants to transfer 1 `DAI` from Ethereum to Optimism.
- He provides 1 USDC and swaps it for 0.9522424224222 DAI, with the [`externalSwap`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L101).
- [A fee](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L138) is calculated as `fee = (bridgeAmount * srcExternalFeeRate) / FEE_BASE;`.

```solidity
function conversionRateTest() public {
    uint256 value = 0.9522424224222 * 10**18;
    uint256 x = (value - (value * 25 / 10**5));
    uint256 y = (x / uint256(10**12)) * uint256(10**12);
    console2.log("x:", x); // 952004361816594450
    console2.log("y:", y); // 952004000000000000
}
```
- Fee is deducted from the bridgeAmount and he expects to receive: `952004361816594450`.
- But as this value is not a multiple of the convert rate he receives: `952004000000000000`. 
- The difference of `361816594450` is left inside the `WooCrossChainRouterV4` contract.

Another issue is that the allowance is set for the amount prior to convert rate trimming:
```solidity
if (srcInfos.bridgeToken == weth) {
    IWETH(weth).withdraw(bridgeAmount);
    msgValue += bridgeAmount;
} else {
>>>>    TransferHelper.safeApprove(srcInfos.bridgeToken, sgInfo.sgRouter(), bridgeAmount);
}
```

But the StargateRouter only transfers the trimmed amount: 
```soldity
## StargateRouter.sol

function swap(
        uint16 _dstChainId,
        uint256 _srcPoolId,
        uint256 _dstPoolId,
        address payable _refundAddress,
        uint256 _amountLD,
        uint256 _minAmountLD,
        lzTxObj memory _lzTxParams,
        bytes calldata _to,
        bytes calldata _payload
    ) external payable override nonReentrant {
        require(_amountLD > 0, "Stargate: cannot swap 0");
        require(_refundAddress != address(0x0), "Stargate: _refundAddress cannot be 0x0");
        Pool.SwapObj memory s;
        Pool.CreditObj memory c;
        {
            Pool pool = _getPool(_srcPoolId);
            {
>>>                uint256 convertRate = pool.convertRate();
>>>                _amountLD = _amountLD.div(convertRate).mul(convertRate);
            }

            s = pool.swap(_dstChainId, _dstPoolId, msg.sender, _amountLD, _minAmountLD, true);
            _safeTransferFrom(pool.token(), msg.sender, address(pool), _amountLD);
            c = pool.sendCredits(_dstChainId, _dstPoolId);
        }
        bridge.swap{value: msg.value}(_dstChainId, _srcPoolId, _dstPoolId, _refundAddress, c, s, _lzTxParams, _to, _payload);
    }

```

Which means there is a dangling allowance left to the StargateRouter contract. This can be an issue with tokens such as `USDT` that don't allow setting non-zero to non-zero allowance as all the next transfers of this token will revert, causing a long-term DoS. 


## Impact
Stargate Pool conversion rate leads to loss of tokens for the user and long-term DoS for the next transfers for `USDT` token and other tokens that don't allow setting non-zero to non-zero allowances.
As this is highly likely to occur on most transfers, and it has medium impact I'm rating this as a medium-severity issue. 


## Code Snippet
- https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L256

## Tool used

Manual Review

## Recommendation
One solution is for the fee amount to be equal to the convert rate dust removal amount. This way there will be no allowances left and the user will not be overcharged.

