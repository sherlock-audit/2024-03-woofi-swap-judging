Genuine White Otter

medium

# If chainlink oracle data feed is blocked, price cannot be updated for that token by WoOracle price

## Summary

If chainlink oracle data feed is blocked, price cannot be updated for that token by WoOracle price

## Vulnerability Detail

when the protocol wants to update the price of a base token using the ```price``` function in ```WooracleV2_2.sol```, it involves checking the wooracle price and the chainlink price which is from a chainlink price feed. If the chainlink oracle price lookup reverts then the whole function reverts, even if there was a valid wooracle price that could have been used instead

Chainlink has taken oracles offline in extreme cases. For example, during the UST collapse, Chainlink paused the UST/ETH price oracle. 
 
Chainlink’s multisigs can immediately block access to price feeds at will. 

The following ```price``` function in ```WooracleV2_2.sol``` 

```solidity
function price(address _base) public view override returns (uint256 priceOut, bool feasible) {
        uint256 woPrice_ = uint256(infos[_base].price);
        uint256 woPriceTimestamp = timestamp;

        (uint256 cloPrice_, ) = _cloPriceInQuote(_base, quoteToken);

        bool woFeasible = woPrice_ != 0 && block.timestamp <= (woPriceTimestamp + staleDuration);
        bool woPriceInBound = cloPrice_ == 0 ||
            ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);

        if (woFeasible) {
            priceOut = woPrice_;
            feasible = woPriceInBound;
        } else {
            priceOut = clOracles[_base].cloPreferred ? cloPrice_ : 0;
            feasible = priceOut != 0;
        }
    }
```

calls the internal function ```_cloPriceInQuote``` 

```solidity
function _cloPriceInQuote(address _fromToken, address _toToken)
        internal
        view
        returns (uint256 refPrice, uint256 refTimestamp)
    {
        address baseOracle = clOracles[_fromToken].oracle;
        if (baseOracle == address(0)) {
            return (0, 0);
        }
        address quoteOracle = clOracles[_toToken].oracle;
        uint8 quoteDecimal = clOracles[_toToken].decimal;

        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
        (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
        uint256 baseRefPrice = uint256(rawBaseRefPrice);
        uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

        // NOTE: Assume wooracle token decimal is same as chainlink token decimal. 
        uint256 ceoff = uint256(10)**quoteDecimal;
        refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
        refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt; 
    }
```

if any of the following lines reverts due to the reasons previously stated, all calls to update the price via the ```price``` function will fail even if the other oracle can provide a new price

```solidity
(, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
(, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
```

## Impact

Unable to update prices with wooracle when chainlink oracle is unavailable.

## Code Snippet
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L243-L260
## Tool used

Manual Review

## Recommendation 

it is recommended to query ChainLink price feeds using a defensive approach with Solidity’s try/catch [structure](https://docs.soliditylang.org/en/latest/control-structures.html#try-catch). In this way, if the call to the price feed fails, the caller contract is still in control and can handle any errors safely and explicitly.
