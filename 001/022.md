Blurry Gingerbread Lion

medium

# Chainlink price oracle can be zero or negative, but does not check for it

## Summary

`latestRoundData` can return 0 or negative, but `_cloPriceInQuote` casts it to uint256 without checking.

## Vulnerability Detail

Chainlink oracle's `latestRoundData` can [return a negative](https://docs.chain.link/data-feeds/api-reference#latestrounddata). However, WooracleV2_2 casts it to uint256 without checking if it's negative. This could lead to using the wrong price.

Also, if the `cloPrice_` value is 0, `woPriceInBound` is set to true. This is to avoid checking if the value is within range when not using Chainlink oracle. However, `latestRoundData` can also return 0. When it returns 0, `woPriceInBound` is incorrectly set.

```solidity
function _cloPriceInQuote(address _fromToken, address _toToken)
    internal
    view
    returns (uint256 refPrice, uint256 refTimestamp)
{
    address baseOracle = clOracles[_fromToken].oracle;
    if (baseOracle == address(0)) {
@>      return (0, 0);
    }
    address quoteOracle = clOracles[_toToken].oracle;
    uint8 quoteDecimal = clOracles[_toToken].decimal;

@>  (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
@>  (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
@>  uint256 baseRefPrice = uint256(rawBaseRefPrice);
@>  uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

    // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
    uint256 ceoff = uint256(10)**quoteDecimal;
    refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
    refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
}

function price(address _base) public view override returns (uint256 priceOut, bool feasible) {
    uint256 woPrice_ = uint256(infos[_base].price);
    uint256 woPriceTimestamp = timestamp;

@>  (uint256 cloPrice_, ) = _cloPriceInQuote(_base, quoteToken);

    bool woFeasible = woPrice_ != 0 && block.timestamp <= (woPriceTimestamp + staleDuration);
@>  bool woPriceInBound = cloPrice_ == 0 ||
        ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);

    if (woFeasible) {
        priceOut = woPrice_;
        feasible = woPriceInBound;
    } else {
@>      priceOut = clOracles[_base].cloPreferred ? cloPrice_ : 0;
        feasible = priceOut != 0;
    }
}
```

## Impact

The wrong price can be used or `woPriceInBound` check can be bypassed.

## Code Snippet

[https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L360-L363](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L360-L363)

[https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L247-L251](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L247-L251)

## Tool used

Manual Review

## Recommendation

```diff
function _cloPriceInQuote(address _fromToken, address _toToken)
    internal
    view
    returns (uint256 refPrice, uint256 refTimestamp)
{
    address baseOracle = clOracles[_fromToken].oracle;
    if (baseOracle == address(0)) {
        return (0, 0);
    }
    address quoteOracle = clOracles[_toToken].oracle;
    uint8 quoteDecimal = clOracles[_toToken].decimal;

    (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
    (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
+   require(rawBaseRefPrice > 0, "wrong price");
+   require(rawQuoteRefPrice > 0, "wrong price");
    uint256 baseRefPrice = uint256(rawBaseRefPrice);
    uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

    // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
    uint256 ceoff = uint256(10)**quoteDecimal;
    refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
    refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
}
```