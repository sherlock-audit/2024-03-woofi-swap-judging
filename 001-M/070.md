Agreeable Orange Griffin

medium

# Oracle Trust

## Summary

The absence of checks for stale data in Chainlink price feeds within `WooracleV2_2.sol` poses a significant risk to users. Without verification of whether the returned prices are up-to-date, users may execute swaps based on outdated information, leading to potential losses or gains in tokens.

## Vulnerability Detail

In `WooracleV2_2.sol`, the calculation of token prices in USD relies on Chainlink Price Feeds. However, the smart contract lacks mechanisms to verify the freshness of the returned price data. This oversight creates vulnerabilities as users may make trading decisions using stale prices, resulting in inaccurate outcomes and potential financial losses.

Additionally, Chainlink aggregators have a built in circuit breaker if the price of an asset goes outside of a predetermined price band. The result is that if an asset experiences a huge drop in value (i.e. LUNA crash) the price of the oracle will continue to return the `minPrice` instead of the actual price of the asset. This would allow user to continue buying the asset but at the wrong price. This is exactly what happened to [Venus on BSC when LUNA imploded.](https://rekt.news/venus-blizz-rekt/)

Example: TokenA has a minPrice of `$1`. The price of TokenA drops to `$0.10`. The aggregator still returns `$1` allowing the user to buy TokenA as if it is `$1` which is 10x it's actual value.

```solidity
    function _cloPriceInQuote(address _fromToken, address _toToken)
        internal
        view
        returns (uint256 refPrice, uint256 refTimestamp)
    {
        address baseOracle = clOracles[_fromToken].oracle;
        if (baseOracle == address(0)) {
            return (0, 0);
        }
        address quoteOracle = clOracles[_toToken].oracle;
        uint8 quoteDecimal = clOracles[_toToken].decimal;


@>        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
@>       (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
@>        uint256 baseRefPrice = uint256(rawBaseRefPrice);
@>        uint256 quoteRefPrice = uint256(rawQuoteRefPrice);


        // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
        uint256 ceoff = uint256(10)**quoteDecimal;
        refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
        refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
    }
```

GitHub: [[348-369](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L348C1-L369C6)]

Lastly, the conversion of int256 prices to uint256 within `_cloPriceInQuote` could result in unexpected behavior if negative prices are ever encountered. This conversion effectively transforms negative numbers into positive ones, potentially distorting price calculations and leading to further inaccuracies. For example, when int(-1e8)  is converted to uint, it will return the following result:

```bash
$ chisel
Welcome to Chisel! Type `!help` to show available commands.
➜ uint(int(-1e8))
Type: uint
├ Hex: 0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffa0a1f00
└ Decimal: 115792089237316195423570985008687907853269984665640564039457584007913029639936
➜
```



## Impact

The vulnerability exposes users to potential losses or gains due to inaccurate price information obtained from Chainlink price feeds

## Code Snippet

#### PoC showing the vulnerability when BTC price hit's `minAnswer`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {WooPPV2} from "contracts/WooPPV2.sol";
import {ChainlinkOracle} from "test/foundry/ChainlinkOracle.sol";
import {TestUsdtToken} from "contracts/test/TestUsdtToken.sol";
import {TestERC20Token} from "contracts/test/TestERC20Token.sol";
import {WETH} from "contracts/test/Weth.sol";
import {WooracleV2_2} from "contracts/wooracle/WooracleV2_2.sol";
import {WooRouterV2} from "contracts/WooRouterV2.sol";
import {WooCrossChainRouterV4} from "contracts/CrossChain/WooCrossChainRouterV4.sol";
import {IWooCrossChainRouterV3} from "contracts/interfaces/CrossChain/IWooCrossChainRouterV3.sol";
import {IWooCrossRouterForWidget} from "contracts/interfaces/CrossChain/IWooCrossRouterForWidget.sol";
import {SgInfo} from "contracts/CrossChain/SgInfo.sol";
import {WooCrossRouterForWidget} from "contracts/CrossChain/WooCrossRouterForWidget.sol";
import {NonceCounter} from "contracts/WOOFiDex/NonceCounter.sol";

contract WooFiTest is Test{
    address public feeAddress;
    address public owner;

    struct Contracts{
        WooPPV2 wooPPV2;
        TestUsdtToken usdtToken;
        TestERC20Token btcToken;
        TestERC20Token arbToken;
        WooracleV2_2 wooracleV2_2;
        WooCrossRouterForWidget wooCrossRouterForWidget;
        WooCrossChainRouterV4 wooCrossChainRouterV4;
        NonceCounter nonceCounter;
        WooRouterV2 wooRouterV2;
        WETH weth;
        SgInfo sgInfo;
    }

    struct Oracles{
        ChainlinkOracle arbChainlinkOracle;
        ChainlinkOracle btcChainlinkOracle;
        ChainlinkOracle usdtChainlinkOracle;
    }

    Contracts public contracts;
    Oracles public oracles;

    function setUp() public {
        feeAddress = makeAddr("feeAddress");
        owner = makeAddr("owner");

        // quote token
        contracts.usdtToken = new TestUsdtToken();

        // base tokens: btc
        contracts.btcToken = new TestERC20Token();

        // base tokens: arb
        contracts.arbToken = new TestERC20Token();

        // weth
        contracts.weth = new WETH();

        // woo ppv2
        contracts.wooPPV2 = new WooPPV2(address(contracts.usdtToken));
        
        // setting chainlink oracles
        oracles.arbChainlinkOracle = new ChainlinkOracle("ARB / USD", 500e8);
        oracles.btcChainlinkOracle = new ChainlinkOracle("BTC / USD", 40000e8);
        oracles.usdtChainlinkOracle = new ChainlinkOracle("USDT / USD", 1e8);

        // wooracle
        contracts.wooracleV2_2 = new WooracleV2_2();
        contracts.wooracleV2_2.setCLOracle(address(contracts.arbToken), address(oracles.arbChainlinkOracle), true);
        contracts.wooracleV2_2.setCLOracle(address(contracts.btcToken), address(oracles.btcChainlinkOracle), true);
        contracts.wooracleV2_2.setQuoteToken(address(contracts.usdtToken), address(oracles.usdtChainlinkOracle));
        contracts.wooracleV2_2.setBound(1e9);

        // set wooracle
        contracts.wooPPV2.init(address(contracts.wooracleV2_2), feeAddress);
        contracts.wooPPV2.setFeeRate(address(contracts.btcToken), 100);
        contracts.wooPPV2.setMaxGamma(address(contracts.btcToken), 0.1 ether);
        contracts.wooPPV2.setMaxGamma(address(contracts.arbToken), 0.1 ether);
        contracts.wooPPV2.setFeeRate(address(contracts.usdtToken), 100);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.btcToken), 50000 ether);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.usdtToken), 50000 ether);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.arbToken), 50000 ether);
        
        contracts.btcToken.mint(address(this), 100000 ether);
        contracts.usdtToken.mint(address(this), 100000 ether);
        contracts.arbToken.mint(address(this), 100000 ether);


        contracts.btcToken.approve(address(contracts.wooPPV2), 100000 ether);
        contracts.usdtToken.approve(address(contracts.wooPPV2), 100000 ether);
        contracts.arbToken.approve(address(contracts.wooPPV2), 100000 ether);
        
        contracts.wooPPV2.deposit(address(contracts.btcToken), 100000 ether);
        contracts.wooPPV2.deposit(address(contracts.usdtToken), 100000 ether);
        contracts.wooPPV2.deposit(address(contracts.arbToken), 100000 ether);

        // setting wooracle state
        contracts.wooracleV2_2.postState(address(contracts.btcToken), 20000 * 10 ** 8, 0.001 ether, 0.000000001 ether);
        contracts.wooracleV2_2.postState(address(contracts.arbToken), 550 * 10 ** 8, 0.001 ether, 0.000000001 ether);

        // setting admin
        contracts.wooracleV2_2.setAdmin(address(contracts.wooPPV2), true);

        // setting cross chain router
        contracts.nonceCounter = new NonceCounter();
        contracts.wooRouterV2 = new WooRouterV2(address(contracts.weth), address(contracts.wooPPV2));
        contracts.sgInfo = new SgInfo(makeAddr("SgRouter"), 101 /* represent ethereum */);
        contracts.wooCrossChainRouterV4 = new WooCrossChainRouterV4(address(contracts.weth), address(contracts.wooRouterV2),address(contracts.sgInfo));

        contracts.nonceCounter.setCrossChainRouter(address(contracts.wooCrossChainRouterV4), true);
        contracts.nonceCounter.setCrossChainRouter(address(contracts.wooCrossRouterForWidget), true);

        // setting cross chain widget
        contracts.wooCrossRouterForWidget = new WooCrossRouterForWidget(address(contracts.nonceCounter), address(contracts.wooRouterV2), address(contracts.wooCrossChainRouterV4), address(contracts.sgInfo));
    }

      function test_oralcesCanCauseIssues() public {
        // setup
        address alice = makeAddr("alice");
        uint256 amount = 1 * 10 ** contracts.btcToken.decimals();

        // setting clopreffered for btc to true
        contracts.wooracleV2_2.setCloPreferred(address(contracts.btcToken), true);

        // setting price of btc lower than min price of btc
        // let's say price of btc is moved down by 10000 usd due to infavourable conditions
        // updating the price of btc
        contracts.wooracleV2_2.postPrice(address(contracts.btcToken), 30000e8, block.timestamp);

        // but there was a min Answer limit of 35000e8 in chainlink oracle. so it would always return to 35000e8 now
        oracles.btcChainlinkOracle.setRoundData(35000e8);

        // moving few minutes in future to simulate the time passed so that chainlink feeds can be used
        skip(3 minutes);

        // minting tokens to alice
        contracts.btcToken.mint(alice, amount);

        // setting price of btc very high

        // alice transfers tokens to the woopp
        vm.startPrank(alice);
        contracts.btcToken.transfer(address(contracts.wooPPV2), amount);


        // alice tries to swap btc to usdt
        uint256 amountRecieved = contracts.wooPPV2.swap(address(contracts.btcToken), address(contracts.usdtToken), amount, 0, alice, alice);

        (,int256 btcOraclePrice,,,)= oracles.btcChainlinkOracle.latestRoundData();
        (,int256 usdtOraclePrice,,,)= oracles.usdtChainlinkOracle.latestRoundData();
        console2.log("Swapping %s btc to usdt", amount);
        console2.log("Chainlink price of btc: %s", btcOraclePrice);
        console2.log("Chainlink price of usdt: %s", usdtOraclePrice);
        console2.log("Market price of btc: 35000e8");
        console2.log("Approx Amount should have been received by alice (including fee): %s", (amount * uint256(30000e8) / uint256(usdtOraclePrice)) / (10 ** (contracts.btcToken.decimals() - contracts.usdtToken.decimals())));
        console2.log("Amount received by alice: %s", amountRecieved);
    }

}
```

#### Output
```bash
[PASS] test_oralcesCanCauseIssues() (gas: 275087)
Logs:
  Swapping 1000000000000000000 btc to usdt
  Chainlink price of btc: 3500000000000
  Chainlink price of usdt: 100000000
  Market price of btc: 35000e8
  Approx Amount should have been received by alice (including fee): 30000000000
  Amount received by alice: 34928811225

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.81ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tool used

- Manual Review
- Foundry

## Recommendation

It is recommended to add the following changes:

```diff
    function _cloPriceInQuote(address _fromToken, address _toToken)
        internal
        view
        returns (uint256 refPrice, uint256 refTimestamp)
    {
        address baseOracle = clOracles[_fromToken].oracle;
        if (baseOracle == address(0)) {
            return (0, 0);
        }
        address quoteOracle = clOracles[_toToken].oracle;
        uint8 quoteDecimal = clOracles[_toToken].decimal;

-        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
-        (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
+        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = getAssetPriceInUsd(baseOracle);
+        (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = getAssetPriceInUsd(quoteOracle);

        uint256 baseRefPrice = uint256(rawBaseRefPrice);
        uint256 quoteRefPrice = uint256(rawQuoteRefPrice);


        // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
        uint256 ceoff = uint256(10)**quoteDecimal;
        refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
        refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
    }

+    uint256 validPeriod = 2 hours;  // or anything acceptable to the protocol
+    mapping(address priceFeed => int256 minPrice) minAnswerForAssetFeed;
+    mapping(address priceFeed => int256 maxPrice) maxAnswerForAssetFeed;

+    function getAssetPriceInUsd(address chainlinkPriceFeed ) public  returns(uint256, uint256){
+        (uint80 roundId, int256 answer, , uint256 updatedAt, uint80 answeredInRound) =Chainlink.AggregatorV3Interface(chainlinkPriceFeed).latestRoundData();

+        require(answer > 0, "Price less than 0");
+        require(block.timestamp - updatedAt < validPeriod, "freshness check failed.")
+        require(answer > minAnswerForAssetFeed(chainlinkPriceFeed), "Min price exceeded");
+        require(answer < maxAnswerForAssetFeed(chainlinkPriceFeed), "Max price exceeded");
+        require(answeredInRound >= roundID, "Stale price");
+        return (uint256(answer), updatedAt);
+    }
```

