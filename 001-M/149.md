Fancy Latte Jaguar

medium

# Ignoring Chainlink Timestamps Leads to Price Innaccuracy in WooracleV2_2

## Summary
The `WooracleV2_2` contract potentially allows for stale prices to be used in its price comparison logic. This issue arises from ignoring the `refTimestamp` timestamp provided by Chainlink price feeds, leading to situations where an outdated price could be used for swapping if the `TokenInfo` price is stale.

## Vulnerability Detail
In the `price` function, the contract retrieves the token price from the `TokenInfo` and checks its feasibility based on whether the price is non-zero and the current time is within a certain duration (`staleDuration`) from the last update timestamp. 
```solidity
function price(address _base) public view override returns (uint256 priceOut, bool feasible) {
    uint256 woPrice_ = uint256(infos[_base].price);
    uint256 woPriceTimestamp = timestamp;

    (uint256 cloPrice_, ) = _cloPriceInQuote(_base, quoteToken);

    bool woFeasible = woPrice_ != 0 && block.timestamp <= (woPriceTimestamp + staleDuration);
    bool woPriceInBound = cloPrice_ == 0 ||
        ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);

    if (woFeasible) {
        priceOut = woPrice_;
        feasible = woPriceInBound;
    } else {
        priceOut = clOracles[_base].cloPreferred ? cloPrice_ : 0;
        feasible = priceOut != 0;
    }
}
```
Additionally, it compares it with the price retrieved from the Chainlink price feed, if there is a price difference more than `bound` (1% by default), it returns `false` in the `feasible` parameter making the price unusable.

However, the `refTimestamp` returned by `_cloPriceInQuote` (which the oldest one from the two price feeds) is not utilized. This oversight can lead to scenarios where the price is considered valid even if it's actually stale according to the Chainlink price feed.
```solidity
function _cloPriceInQuote(address _fromToken, address _toToken)
    internal
    view
    returns (uint256 refPrice, uint256 refTimestamp)
{
    address baseOracle = clOracles[_fromToken].oracle;
    if (baseOracle == address(0)) {
        return (0, 0);
    }
    address quoteOracle = clOracles[_toToken].oracle;
    uint8 quoteDecimal = clOracles[_toToken].decimal;

    (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
    (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
    uint256 baseRefPrice = uint256(rawBaseRefPrice);
    uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

    // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
    uint256 ceoff = uint256(10)**quoteDecimal;
    refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
    refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
}
```

## Impact
This lead to situations where an outdated price could be used for swapping if the `TokenInfo` price is stale, which can result in a loss to the user if the stale price is higher than the current one, or a loss to the protocol where the user can extract value exploiting the lower stale price.

## Code Snippet
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L247
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L348-L369

## Tool used

Manual Review

## Recommendation
It is recommended to perform a stale check over the `refTimestamp` to prevent stale prices from being taken into consideration.