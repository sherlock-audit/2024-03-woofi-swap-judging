Damp Wintergreen Seagull

medium

# `WooracleV2_2.postPrice()`: updating the price of one base token would result in falsely updating the freshness of all base tokens

## Summary

`WooracleV2_2.postPrice()` function updates the `timestamp` variable that is used to check the freshness of the saved base token price when the price of one base token is updated which will result in falsely updating the freshness of all base tokens price that haven't been updated for a while.

## Vulnerability Detail

- `WooracleV2_2.postPrice()` function is supposed to be called by the `WooPPV2` contract whenever a trade is made:

  - when a user sells his quote token for one base token, which will result in decreasing the reserve of that bought base token and hence increasing its price.
  - when a user sells one of the whitelisted base tokens to get quote tokens in return, and this will result in increasing the reserve of that sold base token and hence decreasing its price.

    ```javascript
        function _sellBase(
                address baseToken,
                uint256 baseAmount,
                uint256 minQuoteAmount,
                address to,
                address rebateTo
            ) private nonReentrant whenNotPaused returns (uint256 quoteAmount) {
            //some code...
                    (quoteAmount, newPrice) = _calcQuoteAmountSellBase(baseToken, baseAmount, state);
                    IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
            //some code...
            }
    ```

    ```javascript
        function _sellQuote(
                address baseToken,
                uint256 quoteAmount,
                uint256 minBaseAmount,
                address to,
                address rebateTo
            ) private nonReentrant whenNotPaused returns (uint256 baseAmount) {
                //some code...
                    IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken);
                    (baseAmount, newPrice) = _calcBaseAmountSellQuote(baseToken, quoteAmount, state);
                    IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
                //some code...
            }
    ```

    where `IWooracleV2(wooracle).postPrice()` will update the price of the base token and the `timestamp` variable that's used for staleness check when the price of the base token is extracted by `WooracleV2_2.price()` **will only be updated if the caller is not the WOOPPV2 contract**:

    ```javascript
        function postPrice(address _base, uint128 _price, uint256 _ts) external onlyAdmin {
            infos[_base].price = _price;
            timestamp = _ts;
        }
    ```

    ```javascript
        function price(address _base) public view override returns (uint256 priceOut, bool feasible) {
            uint256 woPrice_ = uint256(infos[_base].price);
            uint256 woPriceTimestamp = timestamp;

            (uint256 cloPrice_, ) = _cloPriceInQuote(_base, quoteToken);

            bool woFeasible = woPrice_ != 0 && block.timestamp <= (woPriceTimestamp + staleDuration);
            bool woPriceInBound = cloPrice_ == 0 ||
                ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);

            if (woFeasible) {
                priceOut = woPrice_;
                feasible = woPriceInBound;
            } else {
                priceOut = clOracles[_base].cloPreferred ? cloPrice_ : 0;
                feasible = priceOut != 0;
            }
        }
    ```

- But this implemented logic for using the same `timestamp` when checking the prices of all base tokens is invalid:
  as the price of any base token can be updated by the owner or any other admin, and when one base token is updated: then consuming the updated `timestamp` to check the price freshness of the other un-updated/stale base token prices would result in using stale prices to evaluate the price of that asset (as they are falsely indicated as fresh prices).

## Impact

This would cause a serious issue if the saved base price hasn't been updated for a while, where it will be considered valid (`woFeasible == true`) while in cases of a large price movements; it will be considered as out of bounds of the returned oracle price (`woPriceInBound == false`), thus the price feasibility would be set to `false` (`feasible == woPriceInBound == false`), which will result in reverting any transaction to trade for that base token as the `WooPPV2` contract checks for the price feasibility before proceeding with the trade, for example:

```javascript
    function _calcBaseAmountSellQuote(
            address baseToken,
            uint256 quoteAmount,
            IWooracleV2.State memory state
        ) private view returns (uint256 baseAmount, uint256 newPrice) {
            require(state.woFeasible, "WooPPV2: !ORACLE_FEASIBLE");
            //the rest of the function...
        }
```

## Code Snippet

[WooracleV2_2.postPrice function](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L139C1-L144C6)

```javascript
    function postPrice(address _base, uint128 _price, uint256 _ts) external onlyAdmin {
        infos[_base].price = _price;
        timestamp = _ts;
    }
```

## Tool used

Manual Review

## Recommendation

Save the timestamp of the latest update for each base token separately , this is done by updating `TokenInfo` struct:

```diff
    struct TokenInfo {
        uint128 price;
        uint64 coeff; 8
        uint64 spread;
+       uint256 timestamp;
    }
```

```diff
    function postPrice(address _base, uint128 _price) external onlyAdmin {
        infos[_base].price = _price;
        if (msg.sender != wooPP) {
-           timestamp = block.timestamp;
+           infos[_base].timestamp = block.timestamp;
        }
    }
```