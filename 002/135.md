Melodic Navy Goat

medium

# ETH can be stuck in the WooCrossChainRouterV4 contract due to out-of-gas error

## Summary
Receiving ETH on the destination chain can be forced to run out of gas when there is an `externalSwap` involved. This results in the ETH being stuck in the `WooCrossChainRouterV4` contract.

## Vulnerability Detail

One of the possibilities of the [`WooCrossChainRouterV4:crossSwap`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/6518569/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L66) function is to transfer ETH from ChainA to ChainB, and then call the `externalSwap` function on the ChainB to swap ETH into any ERC20 token through a DEX aggregator(1Inch).

### Gas parameters for Stargate

To explore this issue, it's required to understand the gas parameters setting with Stargate.

Stargate Swap allows the caller to specify the:

- `dstGasForCall` which is the gas amount forwarded while calling the `sgReceive` on the destination contract.
- `dstNativeAmount` and `dstNativeAddr` which is the amount and address where the native token is sent to.

For the purpose of this vulnerability only `dstGasForCall` is relevant. This parameter is passed to the `sgReceive` function on the destination chain: 

```solidity
 function _swapRemote(
    uint16 _srcChainId,
    bytes memory _srcAddress,
    uint256 _nonce,
    uint256 _srcPoolId,
    uint256 _dstPoolId,
    uint256 _dstGasForCall,
    address _to,
    Pool.SwapObj memory _s,
    bytes memory _payload
) internal {
    Pool pool = _getPool(_dstPoolId);
    // first try catch the swap remote
    try pool.swapRemote(_srcChainId, _srcPoolId, _to, _s) returns (uint256 amountLD) {
>>>>>>   if (_payload.length > 0) {
            // then try catch the external contract call
>>>>>>      try IStargateReceiver(_to).sgReceive{gas: _dstGasForCall}(_srcChainId, _srcAddress, _nonce, pool.token(), amountLD, _payload) {
                // do nothing
            } catch (bytes memory reason) {
                cachedSwapLookup[_srcChainId][_srcAddress][_nonce] = CachedSwap(pool.token(), amountLD, _to, _payload);
                emit CachedSwapSaved(_srcChainId, _srcAddress, _nonce, pool.token(), amountLD, _to, _payload, reason);
            }
        }
    } catch {
        revertLookup[_srcChainId][_srcAddress][_nonce] = abi.encode(
            TYPE_SWAP_REMOTE_RETRY,
            _srcPoolId,
            _dstPoolId,
            _dstGasForCall,
            _to,
            _s,
            _payload
        );
        emit Revert(TYPE_SWAP_REMOTE_RETRY, _srcChainId, _srcAddress, _nonce);
    }
}
```

As it can be observed the `pool.swapRemote` first transfers the tokens to the destination contract, e.g. in the context of WOOFi it would be the `WooCrossChainRouterV4` contract. 
Then the `sgReceive` function is called on the destination contract with the `dstGasForCall` parameter.

Generally `sgReceive` can fail:

- Due to logical error/revert inside the `sgReceive` function.
- Due to out-of-gas error if the `dstGasForCall` is not enough to execute the `sgReceive` function.

### Vulnerability

An attacker's goal is to make the `sgReceive` function fail due to an out-of-gas error. If that can be achieved the transferred ETH will be stuck in the `WooCrossChainRouterV4` contract resulting in DoS of ETH receiving for the user.  

An out-of-gas error can be achieved by front-running the `sgReceive` function with a transaction that causes the 1inch swap to take the longest path and consume as much gas as possible inside the:

```solidity
## WooCrossChainRouterV4.sol

        if (dst1inch.swapRouter != address(0)) {
            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            uint256 swapAmount = bridgedAmount - fee;
            TransferHelper.safeApprove(weth, address(wooRouter), swapAmount);
            try
>>>>                wooRouter.externalSwap(
                    dst1inch.swapRouter,
                    dst1inch.swapRouter,
                    weth,
                    toToken,
                    swapAmount,
                    minToAmount,
                    payable(to),
                    dst1inch.data
                )
            returns (uint256 realToAmount) {
            
## WooRouterV2.sol
function _internalFallbackSwap(
        address approveTarget,
        address swapTarget,
        address fromToken,
        uint256 fromAmount,
        bytes calldata data
    ) private {

        if (fromToken != ETH_PLACEHOLDER_ADDR) {
            TransferHelper.safeTransferFrom(fromToken, msg.sender, address(this), fromAmount);
            TransferHelper.safeApprove(fromToken, approveTarget, fromAmount);
>>>>            (bool success, ) = swapTarget.call{value: 0}(data);
            TransferHelper.safeApprove(fromToken, approveTarget, 0);
            require(success, "WooRouter: FALLBACK_SWAP_FAILED");
        } 
```

This way the execution flow reaches the `catch` block, but there is not enough gas to execute the:

```solidity           
    } catch {
>>>        TransferHelper.safeApprove(weth, address(wooRouter), 0);
>>>        TransferHelper.safeTransfer(weth, to, bridgedAmount);
        emit WooCrossSwapOnDstChain(
            refId,
            msgSender,
            to,
            weth,
            bridgedAmount,
            toToken,
            weth,
            minToAmount,
            bridgedAmount,
            dst1inch.swapRouter == address(0) ? 0 : 1,
            0
        );
    }
```

As a consequence the whole `sgRecieve` fails, ETH is left sitting in the contract.

There are two facts that make this vulnerability possible: 
- DEX aggregators like 1Inch optimize for the most efficient trading path and in the process might split trade through several DEXes, and this consumes the additional gas to make the `sgReceive` fail due to OOG.
- Due to the plethora of chains you wish to support, some of these implement different gas models, and it's hard to predict the exact amount of gas that will be consumed by the `sgReceive` function.

## Impact

An attacker can front-run the `sgReceive` function to cause out-of-gas error and make the `sgReceive` function fail. This will result in the ETH being stuck in the `WooCrossChainRouterV4` contract.

## Code Snippet

- https://github.com/sherlock-audit/2024-03-woofi-swap/blob/6518569/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L306

## Tool used

Manual Review

## Recommendation
There should be a configurable admin parameter per chain that covers all the gas costs except for the `externalSwap` that is wrapped in a try-catch block. The user should pass the `dstGasForCall` that is provided as gas to the `externalSwap` function.

```diff
## WooCrossChainRouterV4.sol

+    mapping(uint16 => uint256) internal sgReceiveGas;
+ 
     constructor(
@@ -61,6 +63,16 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
         dstExternalFeeRate = 25;
     }
 
+    function setSgReceiveGas(uint16 eid, uint256 gas) external onlyOwner {
+         sgReceiveGas[eid] = gas;
+    }
+
+    function getSgReceiveGas(uint16 eid) internal view returns (uint256) {
+        uint256 gas = sgReceiveGas[eid];
+        if (gas == 0) revert();
+        return gas;
+    }
+
     /* ----- Functions ----- */
 
     function crossSwap(
@@ -165,18 +177,18 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
         require(msg.sender == sgInfo.sgRouter(), "WooCrossChainRouterV3: INVALID_CALLER");
 
         // make sure the same order to abi.encode when decode payload
-        (uint256 refId, address to, address toToken, uint256 minToAmount, Dst1inch memory dst1inch) = abi.decode(
+        (uint256 refId, address to, address toToken, uint256 minToAmount, Dst1inch memory dst1inch, uint256 dstGasForCall) = abi.decode(
             payload,
-            (uint256, address, address, uint256, Dst1inch)
+            (uint256, address, address, uint256, Dst1inch, uint256)
         );
 
         // toToken won't be SGETH, and bridgedToken won't be ETH_PLACEHOLDER_ADDR
         if (bridgedToken == sgInfo.sgETHs(sgInfo.sgChainIdLocal())) {
             // bridgedToken is SGETH, received native token
-            _handleNativeReceived(refId, to, toToken, amountLD, minToAmount, dst1inch);
+            _handleNativeReceived(refId, to, toToken, amountLD, minToAmount, dst1inch, dstGasForCall);
         } else {
             // bridgedToken is not SGETH, received ERC20 token
-            _handleERC20Received(refId, to, toToken, bridgedToken, amountLD, minToAmount, dst1inch);
+            _handleERC20Received(refId, to, toToken, bridgedToken, amountLD, minToAmount, dst1inch, dstGasForCall);
         }
     }
 
@@ -234,13 +246,13 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
             "WooCrossChainRouterV3: !dstInfos.bridgeToken"
         );
 
-        bytes memory payload = abi.encode(refId, to, dstInfos.toToken, dstInfos.minToAmount, dst1inch);
+        bytes memory payload = abi.encode(refId, to, dstInfos.toToken, dstInfos.minToAmount, dst1inch, dstInfos.dstGasForCall);
 
         uint256 dstMinBridgeAmount = (bridgeAmount * (10000 - bridgeSlippage)) / 10000;
         bytes memory dstWooCrossChainRouter = abi.encodePacked(wooCrossRouters[dstInfos.chainId]);
 
         IStargateRouter.lzTxObj memory obj = IStargateRouter.lzTxObj(
-            dstInfos.dstGasForCall,
+            getSgReceiveGas(dstInfos.chainId) + dstInfos.dstGasForCall,
             dstInfos.airdropNativeAmount,
             abi.encodePacked(to)
         );
@@ -272,7 +284,8 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
         address toToken,
         uint256 bridgedAmount,
         uint256 minToAmount,
-        Dst1inch memory dst1inch
+        Dst1inch memory dst1inch,
+        uint256 dstGasForCall
     ) internal {
         address msgSender = _msgSender();
 
@@ -303,7 +316,7 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
             uint256 swapAmount = bridgedAmount - fee;
             TransferHelper.safeApprove(weth, address(wooRouter), swapAmount);
             try
-                wooRouter.externalSwap(
+                wooRouter.externalSwap{gas: dstGasForCall}(
                             dst1inch.swapRouter,
                     dst1inch.swapRouter,
                     weth,
@@ -389,7 +402,8 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
         address bridgedToken,
         uint256 bridgedAmount,
         uint256 minToAmount,
-        Dst1inch memory dst1inch
+        Dst1inch memory dst1inch,
+        uint256 dstGasForCall
     ) internal {
         address msgSender = _msgSender();
 
@@ -416,7 +430,7 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
             TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
             if (dst1inch.swapRouter != address(0)) {
                 try
-                    wooRouter.externalSwap(
+                    wooRouter.externalSwap{gas: dstGasForCall}(
                         dst1inch.swapRouter,
                         dst1inch.swapRouter,
                         bridgedToken,
```

