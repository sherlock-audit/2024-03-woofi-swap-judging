Agreeable Orange Griffin

high

# Loss of Tokens Due to Incorrect Fee Deduction in `WooCrossChainRouterV4`

## Summary

The vulnerability in `WooCrossChainRouterV4` causes a loss of tokens to users by deducting fees for WooFi's internal swaps.

## Vulnerability Detail

In `WooCrossChainRouterV4`, extra fees are charged from the swap amount when an external provider is used for the swap. However, the `WooCrossChainRouterV4::_handleERC20Received(...)` function mistakenly deducts fees even when the WooFi's internal Swap is utilized.

```solidity
File: 2024-03-woofi-swap-Aamirusmani1552/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol

    function _handleERC20Received(
        uint256 refId,
        address to,
        address toToken,
        address bridgedToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {

        ...

        else {
            // Deduct the external swap fee
@-1->            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            bridgedAmount -= fee;
            
            if (dst1inch.swapRouter != address(0)) {    
       
                ...

         
            } else {
@-2->                try wooRouter.swap(bridgedToken, toToken, bridgedAmount, minToAmount, payable(to), to) returns (
                    uint256 realToAmount
                ) {
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        toToken,
                        minToAmount,
                        realToAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                } catch {
  @-3->                  TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        bridgedToken,
                        minToAmount,
                        bridgedAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                }
            }
        }
    }

```

GitHub: [[413](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L413), [461](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L461), [478](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L478)]

This vulnerability leads to two potential situations:

1. The swap fails because the `minToAmount` sent is higher than what the user receives after the fee deduction.
2. The swap succeeds with a smaller `minToAmount`, resulting in a loss of tokens for the user.
Even in the first case, if the call fails, the amount sent to the user remains `bridgedAmount - fee` (see `@-3->` in above given code ).


In cases where native tokens are received and a WooFi's swap is required, the fee is not deducted from the bridged amount itself. As a result, the correct amount is sent to the user as you can see here ðŸ‘‡ 

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L302-L303

## Impact

Loss of tokens for users.

## Code Snippet

#### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {WooPPV2} from "contracts/WooPPV2.sol";
import {ChainlinkOracle} from "test/foundry/ChainlinkOracle.sol";
import {TestUsdtToken} from "contracts/test/TestUsdtToken.sol";
import {TestERC20Token} from "contracts/test/TestERC20Token.sol";
import {WETH} from "contracts/test/Weth.sol";
import {WooracleV2_2} from "contracts/wooracle/WooracleV2_2.sol";
import {WooRouterV2} from "contracts/WooRouterV2.sol";
import {WooCrossChainRouterV4} from "contracts/CrossChain/WooCrossChainRouterV4.sol";
import {IWooCrossChainRouterV3} from "contracts/interfaces/CrossChain/IWooCrossChainRouterV3.sol";
import {IWooCrossRouterForWidget} from "contracts/interfaces/CrossChain/IWooCrossRouterForWidget.sol";
import {SgInfo} from "contracts/CrossChain/SgInfo.sol";
import {WooCrossRouterForWidget} from "contracts/CrossChain/WooCrossRouterForWidget.sol";
import {NonceCounter} from "contracts/WOOFiDex/NonceCounter.sol";

contract WooFiTest is Test{
    address public feeAddress;
    address public owner;

    struct Contracts{
        WooPPV2 wooPPV2;
        TestUsdtToken usdtToken;
        TestERC20Token btcToken;
        TestERC20Token arbToken;
        WooracleV2_2 wooracleV2_2;
        WooCrossRouterForWidget wooCrossRouterForWidget;
        WooCrossChainRouterV4 wooCrossChainRouterV4;
        NonceCounter nonceCounter;
        WooRouterV2 wooRouterV2;
        WETH weth;
        SgInfo sgInfo;
    }

    struct Oracles{
        ChainlinkOracle arbChainlinkOracle;
        ChainlinkOracle btcChainlinkOracle;
        ChainlinkOracle usdtChainlinkOracle;
    }

    Contracts public contracts;
    Oracles public oracles;

    function setUp() public {
        feeAddress = makeAddr("feeAddress");
        owner = makeAddr("owner");

        // quote token
        contracts.usdtToken = new TestUsdtToken();

        // base tokens: btc
        contracts.btcToken = new TestERC20Token();

        // base tokens: arb
        contracts.arbToken = new TestERC20Token();

        // weth
        contracts.weth = new WETH();

        // woo ppv2
        contracts.wooPPV2 = new WooPPV2(address(contracts.usdtToken));
        
        // setting chainlink oracles
        oracles.arbChainlinkOracle = new ChainlinkOracle("ARB / USD", 500e8);
        oracles.btcChainlinkOracle = new ChainlinkOracle("BTC / USD", 20000e8);
        oracles.usdtChainlinkOracle = new ChainlinkOracle("USDT / USD", 1e8);

        // wooracle
        contracts.wooracleV2_2 = new WooracleV2_2();
        contracts.wooracleV2_2.setCLOracle(address(contracts.arbToken), address(oracles.arbChainlinkOracle), true);
        contracts.wooracleV2_2.setCLOracle(address(contracts.btcToken), address(oracles.btcChainlinkOracle), true);
        contracts.wooracleV2_2.setQuoteToken(address(contracts.usdtToken), address(oracles.usdtChainlinkOracle));
        contracts.wooracleV2_2.setBound(1e9);

        // set wooracle
        contracts.wooPPV2.init(address(contracts.wooracleV2_2), feeAddress);
        contracts.wooPPV2.setFeeRate(address(contracts.btcToken), 100);
        contracts.wooPPV2.setMaxGamma(address(contracts.btcToken), 0.1 ether);
        contracts.wooPPV2.setMaxGamma(address(contracts.arbToken), 0.1 ether);
        contracts.wooPPV2.setFeeRate(address(contracts.usdtToken), 100);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.btcToken), 50000 ether);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.usdtToken), 50000 ether);
        contracts.wooPPV2.setMaxNotionalSwap(address(contracts.arbToken), 50000 ether);
        
        contracts.btcToken.mint(address(this), 100000 ether);
        contracts.usdtToken.mint(address(this), 100000 ether);
        contracts.arbToken.mint(address(this), 100000 ether);


        contracts.btcToken.approve(address(contracts.wooPPV2), 100000 ether);
        contracts.usdtToken.approve(address(contracts.wooPPV2), 100000 ether);
        contracts.arbToken.approve(address(contracts.wooPPV2), 100000 ether);
        
        contracts.wooPPV2.deposit(address(contracts.btcToken), 100000 ether);
        contracts.wooPPV2.deposit(address(contracts.usdtToken), 100000 ether);
        contracts.wooPPV2.deposit(address(contracts.arbToken), 100000 ether);

        // setting wooracle state
        contracts.wooracleV2_2.postState(address(contracts.btcToken), 20000 * 10 ** 8, 0.001 ether, 0.000000001 ether);
        contracts.wooracleV2_2.postState(address(contracts.arbToken), 550 * 10 ** 8, 0.001 ether, 0.000000001 ether);

        // setting admin
        contracts.wooracleV2_2.setAdmin(address(contracts.wooPPV2), true);

        // setting cross chain router
        contracts.nonceCounter = new NonceCounter();
        contracts.wooRouterV2 = new WooRouterV2(address(contracts.weth), address(contracts.wooPPV2));
        contracts.sgInfo = new SgInfo(makeAddr("SgRouter"), 101 /* represent ethereum */);
        contracts.wooCrossChainRouterV4 = new WooCrossChainRouterV4(address(contracts.weth), address(contracts.wooRouterV2),address(contracts.sgInfo));

        contracts.nonceCounter.setCrossChainRouter(address(contracts.wooCrossChainRouterV4), true);
        contracts.nonceCounter.setCrossChainRouter(address(contracts.wooCrossRouterForWidget), true);

        // setting cross chain widget
        contracts.wooCrossRouterForWidget = new WooCrossRouterForWidget(address(contracts.nonceCounter), address(contracts.wooRouterV2), address(contracts.wooCrossChainRouterV4), address(contracts.sgInfo));
    }

    function test_swapWidget() public{
        // setup
        address alice = makeAddr("alice");
        uint256 amount = 1 * 10 ** contracts.btcToken.decimals();

        // simulating tokens transfered by stargate router to widget contract
        contracts.btcToken.mint(address(contracts.wooCrossChainRouterV4), amount);

        // simulating call by sgRouter
        vm.startPrank(contracts.sgInfo.sgRouter());
        IWooCrossChainRouterV3.Dst1inch memory dstInch = IWooCrossChainRouterV3.Dst1inch({
            swapRouter: address(0),
            data: ""
        });

        // checking the amount that should have been received by alice
        uint256 amountToRecieve = contracts.wooPPV2.tryQuery(address(contracts.btcToken), address(contracts.usdtToken), amount);
        console2.log("Amount to receive: %s", amountToRecieve);
        
        // getting alices balance before the bridging call
        uint256 aliceBalanceBefore = contracts.usdtToken.balanceOf(alice);
        bytes memory payload = abi.encode(uint256(0), alice, address(contracts.usdtToken), 19000 * 10 ** contracts.usdtToken.decimals(), dstInch);
        
        // calling sgRecieve to simulate the token receipt
        contracts.wooCrossChainRouterV4.sgReceive(uint16(0), bytes(""), uint256(0), address(contracts.btcToken), amount, payload);
        
        // getting alices balance after the call
        uint256 aliceBalanceAfter = contracts.usdtToken.balanceOf(alice);

        console2.log("Actual amount recieved by alice: %s", aliceBalanceAfter - aliceBalanceBefore);

        // amount recieved should be less than the actual amount
        assert(amountToRecieve > aliceBalanceAfter - aliceBalanceBefore);
    }

}
```

#### Output:

```bash
Running 1 test for test/foundry/WooFi.t.sol:WooFiTest
[PASS] test_swapWidget() (gas: 280660)
Logs:
  Amount to receive: 19959620400
  Amount Received from Stargate Router: 1000000000000000000
  Swapping amount with Woorouter: 999750000000000000
  Actual amount recieved by alice: 19954630594

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.48ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tool used

- Manual Review
- Foundry

## Recommendation

It's recommended to make the following changes:

```diff
 function _handleERC20Received(
        uint256 refId,
        address to,
        address toToken,
        address bridgedToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {
        address msgSender = _msgSender();

        if (toToken == bridgedToken) {
            TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
            emit WooCrossSwapOnDstChain(
                refId,
                msgSender,
                to,
                bridgedToken,
                bridgedAmount,
                toToken,
                toToken,
                minToAmount,
                bridgedAmount,
                dst1inch.swapRouter == address(0) ? 0 : 1,
                0
            );
        } else {
            // Deduct the external swap fee
            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            bridgedAmount -= fee;

            TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
            if (dst1inch.swapRouter != address(0)) {
                try
                    wooRouter.externalSwap(
                        dst1inch.swapRouter,
                        dst1inch.swapRouter,
                        bridgedToken,
                        toToken,
                        bridgedAmount,
                        minToAmount,
                        payable(to),
                        dst1inch.data
                    )
                returns (uint256 realToAmount) {
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        toToken,
                        minToAmount,
                        realToAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        fee
                    );
                } catch {
                    bridgedAmount += fee;
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        bridgedToken,
                        minToAmount,
                        bridgedAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                }
            } else {
-                try wooRouter.swap(bridgedToken, toToken, bridgedAmount, minToAmount, payable(to), to) returns (
+                try wooRouter.swap(bridgedToken, toToken, bridgedAmount + fee, minToAmount, payable(to), to) returns (
                    uint256 realToAmount
                ) {
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        toToken,
                        minToAmount,
                        realToAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                } catch {
-                   TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
+                   TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        bridgedToken,
                        minToAmount,
                        bridgedAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                }
            }
        }
    }
```
