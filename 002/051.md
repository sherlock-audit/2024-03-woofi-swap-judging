Agreeable Orange Griffin

high

# Loss of User Gas Refunds Due to Incorrect Refund Address Assignment in `WooCrossChainRouterV4`

## Summary

The refund of any extra native gas from the call is directed to the `WooCrossRouterForWidget` instead of the user.

## Vulnerability Detail

In the `WooCrossChainRouterV4::_bridgeByStargate(...)` function, the address of `msg.sender` is sent as the refund address to the `StargateRouter::swap(...)`. This address is used to refund the user any extra gas sent for the bridging call (see [this](https://stargateprotocol.gitbook.io/stargate/developers/how-to-swap#:~:text=msg.sender%2C%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20//%20refund%20adddress.%20extra%20gas%20(if%20any)%20is%20returned%20to%20this%20address)). 

```solidity
File: WooCrossChainRouterV4.sol

function _bridgeByStargate(
        uint256 refId,
        address payable to,
        uint256 msgValue,
        uint256 bridgeAmount,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Dst1inch calldata dst1inch
    ) internal {
         
       ...


        stargateRouter.swap{value: msgValue}(
            dstInfos.chainId, // dst chain id
            sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken), // bridge token's pool id on src chain
            sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken), // bridge token's pool id on dst chain
@>            payable(_msgSender()), // rebate address
            bridgeAmount, // swap amount on src chain
            dstMinBridgeAmount, // min received amount on dst chain
            obj, // config: dstGasForCall, dstAirdropNativeAmount, dstReceiveAirdropNativeTokenAddr
            dstWooCrossChainRouter, // smart contract to call on dst chain
            payload // payload to piggyback
        );
    }
```

GitHub: [[260](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L260))

However, when `WooCrossRouterForWidget::crossSwap(...)` is used for token bridging, it calls the `WooCrossChainRouterV4::crossSwap(...)` function, where `msg.sender` becomes the `WooCrossRouterForWidget` contract. Consequently, any gas refund is directed to this contract instead of the user. 

```solidity
File: WooCrossRouterForWidget.sol

    function crossSwap(
        address payable to,
        IWooCrossChainRouterV3.SrcInfos memory srcInfos,
        IWooCrossChainRouterV3.DstInfos memory dstInfos,
        IWooCrossChainRouterV3.Src1inch calldata src1inch,
        IWooCrossChainRouterV3.Dst1inch calldata dst1inch,
        FeeInfo calldata feeInfo
    ) external payable whenNotPaused nonReentrant {

             ...

@>        crossRouter.crossSwap{value: msgValue}(refId, to, srcInfos, dstInfos, src1inch, dst1inch);
    }
```
GitHub: [[104-134](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossRouterForWidget.sol#L104C1-L134C6)]


## Impact

Users will lose any gas refund.

## Code Snippet

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossRouterForWidget.sol#L133

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L260

## Tool used

- Manual Review

## Recommendation

It is recommended to implement the following changes:

```diff
File: WooCrossChainRouterV4.sol

function crossSwap(
        uint256 refId,
        address payable to,
+        address refundTo, 
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Src1inch calldata src1inch,
        Dst1inch calldata dst1inch
    ) external payable whenNotPaused nonReentrant {

        ...

-        _bridgeByStargate(refId, to, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch);
+        _bridgeByStargate(refId, to, refundTo, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch);

       ...
   }
   

function _bridgeByStargate(
        uint256 refId,
        address payable to,
+        address refundTo,
        uint256 msgValue,
        uint256 bridgeAmount,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Dst1inch calldata dst1inch
    ) internal {


        stargateRouter.swap{value: msgValue}(
            dstInfos.chainId, // dst chain id
            sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken), // bridge token's pool id on src chain
            sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken), // bridge token's pool id on dst chain
-           payable(_msgSender()), // rebate address
+           payable(refundTo), // rebate address
            bridgeAmount, // swap amount on src chain
            dstMinBridgeAmount, // min received amount on dst chain
            obj, // config: dstGasForCall, dstAirdropNativeAmount, dstReceiveAirdropNativeTokenAddr
            dstWooCrossChainRouter, // smart contract to call on dst chain
            payload // payload to piggyback
        );
    }
```


```diff
File: WooCrossRouterForWidget.sol

  function crossSwap(
        address payable to,
+     address refundTo,
        IWooCrossChainRouterV3.SrcInfos memory srcInfos,
        IWooCrossChainRouterV3.DstInfos memory dstInfos,
        IWooCrossChainRouterV3.Src1inch calldata src1inch,
        IWooCrossChainRouterV3.Dst1inch calldata dst1inch,
        FeeInfo calldata feeInfo
    ) external payable whenNotPaused nonReentrant {
      
       ...


-        crossRouter.crossSwap{value: msgValue}(refId, to, srcInfos, dstInfos, src1inch, dst1inch);
+        crossRouter.crossSwap{value: msgValue}(refId, to, refundTo, srcInfos, dstInfos, src1inch, dst1inch);
    }
```