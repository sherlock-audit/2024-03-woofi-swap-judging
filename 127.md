Melodic Navy Goat

medium

# `sgReceive` can be DoSed for token that don't allow setting non-zero to non-zero allowances

## Summary
`sgReceive` can be DoSed for receiving tokens that don't allow setting non-zero to non-zero allowances. The most prominent example is `USDT` token.
This results in the user's tokens being stuck in the `WooCrossChainRouterV4` contract.

## Vulnerability Detail
[`USDT`](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7) token doesn't allow setting non-zero to non-zero allowances. This means if the allowance is non-zero after stargateRouter transfer, next transfers will revert.
```solidity
function approve(address _spender, uint _value) public onlyPayloadSize(2 * 32) {

    // To change the approve amount you first have to reduce the addresses`
    //  allowance to zero by calling `approve(_spender, 0)` if it is not
    //  already 0 to mitigate the race condition described here:
    //  https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
    require(!((_value != 0) && (allowed[msg.sender][_spender] != 0)));
```

If we observe the [`sgReceive`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/6518569/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L179) function logic with bridgedToken being ERC20:
```solidity
    function sgReceive(
        uint16, // srcChainId
        bytes memory, // srcAddress
        uint256, // nonce
        address bridgedToken,
        uint256 amountLD,
        bytes memory payload
    ) external {
        require(msg.sender == sgInfo.sgRouter(), "WooCrossChainRouterV3: INVALID_CALLER");

        // make sure the same order to abi.encode when decode payload
        (uint256 refId, address to, address toToken, uint256 minToAmount, Dst1inch memory dst1inch) = abi.decode(
            payload,
            (uint256, address, address, uint256, Dst1inch)
        );

        // toToken won't be SGETH, and bridgedToken won't be ETH_PLACEHOLDER_ADDR
        if (bridgedToken == sgInfo.sgETHs(sgInfo.sgChainIdLocal())) {
            // bridgedToken is SGETH, received native token
            _handleNativeReceived(refId, to, toToken, amountLD, minToAmount, dst1inch);
        } else {
            // bridgedToken is not SGETH, received ERC20 token
            _handleERC20Received(refId, to, toToken, bridgedToken, amountLD, minToAmount, dst1inch);
        }
    }
    
    function _handleERC20Received(
        uint256 refId,
        address to,
        address toToken,
        address bridgedToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {
        address msgSender = _msgSender();

        if (toToken == bridgedToken) {
            TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
        } else {
            // Deduct the external swap fee
            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            bridgedAmount -= fee;

>>>>            TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
            if (dst1inch.swapRouter != address(0)) {
                try
                wooRouter.externalSwap(
                    dst1inch.swapRouter,
                    dst1inch.swapRouter,
                    bridgedToken,
                    toToken,
                    bridgedAmount,
                    minToAmount,
                    payable(to),
                    dst1inch.data
                )
                returns (uint256 realToAmount) {
                } catch {
                    bridgedAmount += fee;
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                }
            } else {
                try wooRouter.swap(bridgedToken, toToken, bridgedAmount, minToAmount, payable(to), to) returns (
                    uint256 realToAmount
                ) {

                } catch {
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                }
            }
        }
    }
    
```

- In case toToken is not equal to bridgedToken, the `externalSwap` function is called.
- Before the `externalSwap`, approval is granted to the `wooRouter` contract for the `bridgedAmount`.
- If the `externalSwap` fails, the `bridgedAmount` is transferred back to the `to` address, but there is a dangling allowance left for the `wooRouter` contract.

DoS scenario is the following:

- Alice initiates a cross-chain swap from Ethereum to BSC for 1 USDT.
- When she receives the USDT on BSC, she wants to swap it for USDC.
- A griever noticed this transaction, initiates a cross-chain swap from Ethereum to BSC for 2 USDT, but specifies the [`dstGasForCall`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/6518569/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L243) as 0. 
- His intention is to have his transaction fail inside the `sgReceive` and to get saved in the [`cachedSwapLookup`](https://github.com/stargate-protocol/stargate/blob/main/contracts/Router.sol#L285-#L295) mapping, so he can front-run Alice's transaction. 
```solidity
## Router.sol

    function clearCachedSwap(
        uint16 _srcChainId,
        bytes calldata _srcAddress,
        uint256 _nonce
    ) external {
        CachedSwap memory cs = cachedSwapLookup[_srcChainId][_srcAddress][_nonce];
        require(cs.to != address(0x0), "Stargate: cache already cleared");
        // clear the data
        cachedSwapLookup[_srcChainId][_srcAddress][_nonce] = CachedSwap(address(0x0), 0, address(0x0), "");
        IStargateReceiver(cs.to).sgReceive(_srcChainId, _srcAddress, _nonce, cs.token, cs.amountLD, cs.payload);
    }
```
- He front-runs Alice's transaction and initiates the `clearCachedSwap` function to execute his payload.
- His call reverts in the `externalSwap` function, but there is approval left for the `wooRouter` contract for the `bridgedAmount` of 2 USDT.
- Now Alice's transaction will revert as the allowance is non-zero.
- Alice's tokens are stuck in the `WooCrossChainRouterV4` contract. 

## Impact

When a user is transferring `USDT` or any other token that doesn't allow setting non-zero to non-zero allowances, there is a DoS attack vector, resulting in user's tokens being stuck in the `WooCrossChainRouterV4` contract. 
The likelihood of this happening is high as illustrated in the DoS scenario above. The impact is medium. 
I'm marking this as a medium-severity issue.

## Code Snippet

- https://github.com/sherlock-audit/2024-03-woofi-swap/blob/6518569/WooPoolV2/contracts/CrossChain/WooCrossChainRouterV4.sol#L416


## Tool used

Manual Review

## Recommendation
You can utilize the low-level `call` to imitate the same behavior as with try/catch but if `externalSwap` fails the approval is reverted inside the external call. 

```diff
## WooCrossChainRouterV4.sol
 
+    function callExternalSwap(
+        address approveTarget,
+        address swapTarget,
+        address fromToken,
+        address toToken,
+        uint256 fromAmount,
+        uint256 minToAmount,
+        address payable to,
+        bytes calldata data
+    ) public {
+        require(msg.sender == address(this));
+        TransferHelper.safeApprove(fromToken, address(wooRouter), fromAmount);
+        wooRouter.externalSwap(approveTarget, swapTarget, fromToken, toToken, fromAmount, minToAmount, to, data);
+    }
+
     function _handleERC20Received(
         uint256 refId,
         address to,
@@ -413,10 +428,10 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
             uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
             bridgedAmount -= fee;
 
-            TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
             if (dst1inch.swapRouter != address(0)) {
-                try
-                    wooRouter.externalSwap(
+                (bool success,) = address(this).call(
+                    abi.encodeWithSelector(
+                        this.callExternalSwap.selector,
                         dst1inch.swapRouter,
                         dst1inch.swapRouter,
                         bridgedToken,
@@ -426,37 +441,24 @@ contract WooCrossChainRouterV4 is IWooCrossChainRouterV3, Ownable, Pausable, Ree
                         payable(to),
                         dst1inch.data
                     )
-                returns (uint256 realToAmount) {
-                    emit WooCrossSwapOnDstChain(
-                        refId,
-                        msgSender,
-                        to,
-                        bridgedToken,
-                        bridgedAmount,
-                        toToken,
-                        toToken,
-                        minToAmount,
-                        realToAmount,
-                        dst1inch.swapRouter == address(0) ? 0 : 1,
-                        fee
-                    );
-                } catch {
+                );
+                if (!success) {
                     bridgedAmount += fee;
                     TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
-                    emit WooCrossSwapOnDstChain(
-                        refId,
-                        msgSender,
-                        to,
-                        bridgedToken,
-                        bridgedAmount,
-                        toToken,
-                        bridgedToken,
-                        minToAmount,
-                        bridgedAmount,
-                        dst1inch.swapRouter == address(0) ? 0 : 1,
-                        0
-                    );
                 }
+                emit WooCrossSwapOnDstChain(
+                    refId,
+                    msgSender,
+                    to,
+                    bridgedToken,
+                    bridgedAmount,
+                    toToken,
+                    bridgedToken,
+                    minToAmount,
+                    bridgedAmount,
+                    dst1inch.swapRouter == address(0) ? 0 : 1,
+                    0
+                );
```

A similar pattern can be applied for the `wooRouter.swap` function call below. 

