Fun Yellow Kookaburra

high

# Improper Multiple Spread Calculation in BaseToken to BaseToken swap would cause the returned swap value to be inflated

## Summary

Spread calculation in `WooPPV2.sol::_swapBaseToBase()` would cause the returned swap value to be higher than it should be

## Vulnerability Detail

When a user wishes to conduct a base token to base token swap in `WooPPV2.sol::swap()`, [`WooPPV2.sol::_swapBaseToBase()`](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L513C1-L578C6) is called to handle it. Since each base token has its own spread, the highest spread of the two is retrieved and divided by 2. This calculation, however, is guaranteed to return a value less than the spread of these two base tokens.

```solidity
    WooPPV2.sol::_swapBaseToBase()
    function _swapBaseToBase(
        address baseToken1,
        address baseToken2,
        uint256 base1Amount,
        uint256 minBase2Amount,
        address to,
        address rebateTo
    ) private nonReentrant whenNotPaused returns (uint256 base2Amount) {
        ..............
        uint64 spread = _maxUInt64(state1.spread, state2.spread) / 2;
        ..............
    }
```

Going further, this spread is used to calculate the quote or base token amount received during a swap. More specifically, as shown in the snippet below, this is how quoteAmount or baseAmount is calculated during a swap:

```solidity
    WooPPV2.sol::_calcQuoteAmountSellBase()
    function _calcQuoteAmountSellBase(
        address baseToken,
        uint256 baseAmount,
        IWooracleV2.State memory state
    ) private view returns (uint256 quoteAmount, uint256 newPrice) {
        ..............
        quoteAmount =
                (((baseAmount * state.price * decs.quoteDec) / decs.priceDec) *
                    (uint256(1e18) - gamma - state.spread)) /
                1e18 /
                decs.baseDec;
        ..............
    }

    WooPPV2.sol::_calcQuoteAmountSellBase()
    function _calcBaseAmountSellQuote(
        address baseToken,
        uint256 quoteAmount,
        IWooracleV2.State memory state
    ) private view returns (uint256 baseAmount, uint256 newPrice) {
        ..............
        baseAmount =
                (((quoteAmount * decs.baseDec * decs.priceDec) / state.price) *
                    (uint256(1e18) - gamma - state.spread)) /
                1e18 /
                decs.quoteDec;
        ..............
    }

```

Simply translated, the higher the spread, the lower the quoteAmount and vice versa.

## Impact

Assuming baseToken 1 has a spread of 5e17 and baseToken 2 has a spread of 8e17, the max is 8e17. This value divided by 2 returns a value of 4e17, which is lower than the initial spread of both base tokens, yet it is adopted for both tokens. A lower-than-normal value like this will cause the return quote amount or base amount during the swap to be inflated.

## Code Snippet

```solidity
    WooPPV2.sol::_swapBaseToBase()
    function _swapBaseToBase(
        address baseToken1,
        address baseToken2,
        uint256 base1Amount,
        uint256 minBase2Amount,
        address to,
        address rebateTo
    ) private nonReentrant whenNotPaused returns (uint256 base2Amount) {
        require(baseToken1 != address(0) && baseToken1 != quoteToken, "WooPPV2: !baseToken1");
        require(baseToken2 != address(0) && baseToken2 != quoteToken, "WooPPV2: !baseToken2");
        require(to != address(0), "WooPPV2: !to");

        require(balance(baseToken1) - tokenInfos[baseToken1].reserve >= base1Amount, "WooPPV2: !BASE1_BALANCE");
        
        IWooracleV2.State memory state1 = IWooracleV2(wooracle).state(baseToken1);
        IWooracleV2.State memory state2 = IWooracleV2(wooracle).state(baseToken2);

        uint256 swapFee;
        uint256 quoteAmount;
        {
            uint64 spread = _maxUInt64(state1.spread, state2.spread) / 2;
            uint16 feeRate = _maxUInt16(tokenInfos[baseToken1].feeRate, tokenInfos[baseToken2].feeRate);

            state1.spread = spread;
            state2.spread = spread;

            uint256 newBase1Price;
            (quoteAmount, newBase1Price) = _calcQuoteAmountSellBase(baseToken1, base1Amount, state1);
            IWooracleV2(wooracle).postPrice(baseToken1, uint128(newBase1Price));
            swapFee = (quoteAmount * feeRate) / 1e5;
        }

        quoteAmount = quoteAmount - swapFee;
        unclaimedFee = unclaimedFee + swapFee;

        tokenInfos[quoteToken].reserve = uint192(tokenInfos[quoteToken].reserve - swapFee);
        tokenInfos[baseToken1].reserve = uint192(tokenInfos[baseToken1].reserve + base1Amount);

        {
            uint256 newBase2Price;
            (base2Amount, newBase2Price) = _calcBaseAmountSellQuote(baseToken2, quoteAmount, state2);
            IWooracleV2(wooracle).postPrice(baseToken2, uint128(newBase2Price));
            require(base2Amount >= minBase2Amount, "WooPPV2: base2Amount_LT_minBase2Amount");
        }
        
        tokenInfos[baseToken2].reserve = uint192(tokenInfos[baseToken2].reserve - base2Amount);

        if (to != address(this)) {
            TransferHelper.safeTransfer(baseToken2, to, base2Amount);
        }

        emit WooSwap(
            baseToken1,
            baseToken2,
            base1Amount,
            base2Amount,
            msg.sender,
            to,
            rebateTo,
            quoteAmount + swapFee,
            swapFee
        );
    }
```

## Tool used

Manual Review

## Recommendation
Review your spread calculation in `WooPPV2.sol::_swapBaseToBase()`.