Icy Denim Cougar

medium

# Chainlink decimals are not adjusted correctly

## Summary
When the chainlink price retrieved by getting both quote and base tokens price the final answer is not adjusting the base tokens price feed decimals correctly. If the base tokens chainlink price feed is not in 8 decimal precision then the `wooFeasible` will always be "false"
## Vulnerability Detail
Let's assume the quote tokens chainlink price feed returns in 8 decimals and the base tokens chainlink price feed returns 18 decimals. 

Now, considering the above situation, let's put the numbers to the below code snippet where the chainlink ref price is calculated in the oracle:
```solidity
function _cloPriceInQuote(address _fromToken, address _toToken)
        internal
        view
        returns (uint256 refPrice, uint256 refTimestamp)
    {
        address baseOracle = clOracles[_fromToken].oracle;
        if (baseOracle == address(0)) {
            return (0, 0);
        }
        address quoteOracle = clOracles[_toToken].oracle;
        uint8 quoteDecimal = clOracles[_toToken].decimal;

        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
        (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
        uint256 baseRefPrice = uint256(rawBaseRefPrice);
        uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

        // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
        uint256 ceoff = uint256(10)**quoteDecimal;
        refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
        refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
    }
```

`quoteDecimal` = 8
`rawBaseRefPrice` = 18 decimal precised value
`rawQuoteRefPrice` = 8 decimal precised value
`ceoff` = 10 ** 8
`refPrice` = 18 + 8 - 8 = 18 decimal precised value
hence, the return value is 18 decimal precised value

However, the return value has to be in same decimals with the WooOracle decimal precision which is 8 as we can observe here:
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L262-L265

hence, the  `woPriceInBound` will compare a 8 decimal precised price with 18 decimal precised price and will return "false" all the time
```solidity
bool woPriceInBound = cloPrice_ == 0 ||
            ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);
```
## Impact
Some feeds in chainlink can have 18 decimals, if they are ever used as a base token then the woPriceInBound will always be false. If that is "false" woFeasible" will also be "false" hence, the swaps will not be possible in the WooPool.
## Code Snippet
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L114-L123

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L243-L260

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L348-L369

https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L250-L251
## Tool used

Manual Review

## Recommendation
`refPrice = (baseRefPrice * ceoff) / quoteRefPrice / (10 ** (clOracles[_toToken].decimal - 8));`