Damp Wintergreen Seagull

medium

# User's bridged tokens will be permanently stuck in the destination `stargateRouter` contract in some scenarios

## Summary

- User's bridged tokens will be **permanently** stuck in the destination `stargateRouter` contract:

  - if the receiver of the bridged tokens is a blacklisted address by the bridged tokens.
  - if the receiver of the native tokens is a non-payable contract that can't received native asset
  - or if he receiver of the native tokens is a contract that implements a logic that consumes large gas upon receiving these native token.

## Vulnerability Detail

- `WooCrossChainRouterV4.crossSwap()` function is supposed to be called by the users to process cross-chain swapping and bridging, where:

  - the bridging is processed via `stargateRouter` that uses a layerZero endpoint for bridging messages from the source chain to the destination chain where the bridged tokens are going to be received
  - and the cross-chain swapping is done by the `WooCrossChainRouterV4` on the destination chain (if the bridged token is not the same as the received token).

- The transaction flow will be as follows:

  1.  Users call `WooCrossChainRouterV4.crossSwap()` function, where swapping on the source chain is done if required, then a stargate swap call is prepared via `_bridgeByStargate()`:

  ```javascript
  function crossSwap(
        uint256 refId,
        address payable to,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Src1inch calldata src1inch,
        Dst1inch calldata dst1inch
    ) external payable whenNotPaused nonReentrant {
        //some code...
        _bridgeByStargate(refId, to, msgValue, bridgeAmount, srcInfos, dstInfos, dst1inch);
        //some code...
    }


      function _bridgeByStargate(
        uint256 refId,
        address payable to,
        uint256 msgValue,
        uint256 bridgeAmount,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Dst1inch calldata dst1inch
    ) internal {

        //some code...

        bytes memory payload = abi.encode(refId, to, dstInfos.toToken, dstInfos.minToAmount, dst1inch);

        uint256 dstMinBridgeAmount = (bridgeAmount * (10000 - bridgeSlippage)) / 10000;
        //some code...

          stargateRouter.swap{value: msgValue}(
              dstInfos.chainId, // dst chain id
              sgInfo.sgPoolIds(sgInfo.sgChainIdLocal(), srcInfos.bridgeToken), // bridge token's pool id on src chain
              sgInfo.sgPoolIds(dstInfos.chainId, dstInfos.bridgeToken), // bridge token's pool id on dst chain
              payable(_msgSender()), // rebate address
              bridgeAmount, // swap amount on src chain
              dstMinBridgeAmount, // min received amount on dst chain
              obj, // config: dstGasForCall, dstAirdropNativeAmount, dstReceiveAirdropNativeTokenAddr
              dstWooCrossChainRouter, // smart contract to call on dst chain
              payload // payload to piggyback
          );

    }
  ```

  2. Then the bridging is processed via `stargateRouter`, where it uses a layerzero endpoint to bridge messaging between chains, and after the bridging message is checked and verified by layerzero; the [`stargateRouter` on the destination chain](https://github.com/stargate-protocol/stargate/blob/c647a3a647fc693c38b16ef023c54e518b46e206/contracts/Router.sol#L376C5-L426C1) will call `WooCrossChainRouterV4.sgReceive()` function on the destination chain and **transfer the bridged tokens to it (after swapping)**:

  ```javascript
  function swapRemote(
          uint16 _srcChainId,
          bytes memory _srcAddress,
          uint256 _nonce,
          uint256 _srcPoolId,
          uint256 _dstPoolId,
          uint256 _dstGasForCall,
          address _to,
          Pool.SwapObj memory _s,
          bytes memory _payload
      ) external onlyBridge {
          _swapRemote(_srcChainId, _srcAddress, _nonce, _srcPoolId, _dstPoolId, _dstGasForCall, _to, _s, _payload);
      }

      function _swapRemote(
          uint16 _srcChainId,
          bytes memory _srcAddress,
          uint256 _nonce,
          uint256 _srcPoolId,
          uint256 _dstPoolId,
          uint256 _dstGasForCall,
          address _to,
          Pool.SwapObj memory _s,
          bytes memory _payload
      ) internal {
          Pool pool = _getPool(_dstPoolId);
          // first try catch the swap remote
          try pool.swapRemote(_srcChainId, _srcPoolId, _to, _s) returns (uint256 amountLD) {
              if (_payload.length > 0) {
                  // then try catch the external contract call
                  try IStargateReceiver(_to).sgReceive{gas: _dstGasForCall}(_srcChainId, _srcAddress, _nonce, pool.token(), amountLD, _payload) {
                      // do nothing
                  } catch (bytes memory reason) {
                      cachedSwapLookup[_srcChainId][_srcAddress][_nonce] = CachedSwap(pool.token(), amountLD, _to, _payload);
                      emit CachedSwapSaved(_srcChainId, _srcAddress, _nonce, pool.token(), amountLD, _to, _payload, reason);
                  }
              }
          } catch {
              revertLookup[_srcChainId][_srcAddress][_nonce] = abi.encode(
                  TYPE_SWAP_REMOTE_RETRY,
                  _srcPoolId,
                  _dstPoolId,
                  _dstGasForCall,
                  _to,
                  _s,
                  _payload
              );
              emit Revert(TYPE_SWAP_REMOTE_RETRY, _srcChainId, _srcAddress, _nonce);
          }
      }
  ```

  and as can be noticed; the `stargateRouter` implements a non-blocking receive mechanism, where the call to the `WooCrossChainRouterV4.sgReceive()` is done in a try-catch block, and if the transaction reverts for whatever reasons (will be described shortly); the failed transaction will be saved in `cachedSwapLookup[_srcChainId][_srcAddress][_nonce]`, where it can be executed later via [`stargateRouter.clearCachedSwap()`](https://github.com/stargate-protocol/stargate/blob/c647a3a647fc693c38b16ef023c54e518b46e206/contracts/Router.sol#L285C1-L295C6):

  ```javascript
    function clearCachedSwap(
        uint16 _srcChainId,
        bytes calldata _srcAddress,
        uint256 _nonce
    ) external {
        CachedSwap memory cs = cachedSwapLookup[_srcChainId][_srcAddress][_nonce];
        require(cs.to != address(0x0), "Stargate: cache already cleared");
        // clear the data
        cachedSwapLookup[_srcChainId][_srcAddress][_nonce] = CachedSwap(address(0x0), 0, address(0x0), "");
        IStargateReceiver(cs.to).sgReceive(_srcChainId, _srcAddress, _nonce, cs.token, cs.amountLD, cs.payload);
    }
  ```

  3. When the bridged message is received via `WooCrossChainRouterV4.sgReceive()`; the payload is decoded and the bridged tokens are transferred to the receiver address either via `_handleNativeReceived(refId, to, toToken, amountLD, minToAmount, dst1inch)` or `_handleERC20Received(refId, to, toToken, bridgedToken, amountLD, minToAmount, dst1inch)` based on the bridged tokens being the native token of the destination chain or being any ERC20 tokens:

  ```javascript
    function sgReceive(
        uint16, // srcChainId
        bytes memory, // srcAddress
        uint256, // nonce
        address bridgedToken,
        uint256 amountLD,
        bytes memory payload
    ) external {
        require(msg.sender == sgInfo.sgRouter(), "WooCrossChainRouterV3: INVALID_CALLER");

        // make sure the same order to abi.encode when decode payload
        (uint256 refId, address to, address toToken, uint256 minToAmount, Dst1inch memory dst1inch) = abi.decode(
            payload,
            (uint256, address, address, uint256, Dst1inch)
        );

        // toToken won't be SGETH, and bridgedToken won't be ETH_PLACEHOLDER_ADDR
        if (bridgedToken == sgInfo.sgETHs(sgInfo.sgChainIdLocal())) {
            // bridgedToken is SGETH, received native token
            _handleNativeReceived(refId, to, toToken, amountLD, minToAmount, dst1inch);
        } else {
            // bridgedToken is not SGETH, received ERC20 token
            _handleERC20Received(refId, to, toToken, bridgedToken, amountLD, minToAmount, dst1inch);
        }
    }
  ```

  4. For the case when the bridged tokens (that are going to be transferred to the receiver `to` address) are not the native token of the destination chain, and when the `bridgedToken != toToken` ; a swap is done either via an external aggregator (1inch) or via a woofi pool, and if the swap fails, then the `bridgedToken` is transferred directly to the receiver:

  ```javascript
  function _handleERC20Received(
          uint256 refId,
          address to,
          address toToken,
          address bridgedToken,
          uint256 bridgedAmount,
          uint256 minToAmount,
          Dst1inch memory dst1inch
      ) internal {
          address msgSender = _msgSender();

          if (toToken == bridgedToken) {
          //some code...

          } else {
              // Deduct the external swap fee
              uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
              bridgedAmount -= fee;

              TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
              if (dst1inch.swapRouter != address(0)) {
                  try
                      wooRouter.externalSwap(
                          dst1inch.swapRouter,
                          dst1inch.swapRouter,
                          bridgedToken,
                          toToken,
                          bridgedAmount,
                          minToAmount,
                          payable(to),
                          dst1inch.data
                      )
                  returns (uint256 realToAmount) {
                      emit WooCrossSwapOnDstChain(...);

                  } catch {
                      bridgedAmount += fee;
                      TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                      emit WooCrossSwapOnDstChain(...);

                  }
              } else {
                  try wooRouter.swap(bridgedToken, toToken, bridgedAmount, minToAmount, payable(to), to) returns (uint256 realToAmount) {

                      emit WooCrossSwapOnDstChain(...);

                  } catch {
                      TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                      emit WooCrossSwapOnDstChain(...);

                  }
              }
          }
      }
  ```

**So in which cases the bridged tokens will be permanently stuck in the destination `stargateRouter` contract?**
This is the case when **the transaction can't be executed nor retried** (retrying a failed transaction can be done manually; as the failed `sgReceive()` transactions are catched and saved by the `stargateRouter` contract and not by `WooCrossChainRouterV4` contract):

1. If `toToken == bridgedToken` and the receiver address is a blacklisted address by the bridgedToken (**note** that if the swap fails and the user is blacklisted by the bridgedToken, the transaction will not revert as it's catched in a try-catch block, but will result in bridgedTokens being locked in the `WooCrossChainRouterV4` contract until rescued by the owner via `inCaseTokenGotStuck()` and then sent manually to an alternative receiver of the failed transaction, this would require implementing a mechanism to save failed bridging transactions and a mechanism to enable owners of failed transactions to re-specify the receiver address of their bridged tokens in case of failure), as the transfer failure is not catched by a try-catch block by the `_handleERC20Received`, which will result in catching the revert by the `stargateRouter` contract.

2. In `_handleNativeReceived()` : if the `toToken == ETH_PLACEHOLDER_ADDR` and the receiver address (`to`) is either a contract that

   - can't receive native tokens
   - or a contract that implements an extensive logic when receing native asset that consumes the provided gas, which would revert the transfer due to out-of-gas error (as there's no check on if the `dstInfos.dstGasForCall` is sufficient when sending cross-chain swap in the source chain).

## Impact

This would result in a **permanent** lock of the bridged assets in the `stargateRouter` contract as these falied transactions can't be executed again (retried) with the same parameters.

## Code Snippet

[WooCrossChainRouterV4.\_handleERC20Received function/L396-L410](https://github.com/woonetwork/WooPoolV2/blob/a99e13de1492c17a325fff6cddb3696cd7db7dc9/contracts/CrossChain/WooCrossChainRouterV4.sol#L396C9-L410C15)

```javascript
function _handleERC20Received(
        uint256 refId,
        address to,
        address toToken,
        address bridgedToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {
        address msgSender = _msgSender();

        if (toToken == bridgedToken) {
            TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
            emit WooCrossSwapOnDstChain(
                refId,
                msgSender,
                to,
                bridgedToken,
                bridgedAmount,
                toToken,
                toToken,
                minToAmount,
                bridgedAmount,
                dst1inch.swapRouter == address(0) ? 0 : 1,
                0
            );
        } else {
            //some code...
        }
    }
```

[WooCrossChainRouterV4.\_handleNativeReceived function/L279-L296](https://github.com/woonetwork/WooPoolV2/blob/a99e13de1492c17a325fff6cddb3696cd7db7dc9/contracts/CrossChain/WooCrossChainRouterV4.sol#L279C9-L296C10)

```javascript
function _handleNativeReceived(
        uint256 refId,
        address to,
        address toToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {
        address msgSender = _msgSender();

        if (toToken == ETH_PLACEHOLDER_ADDR) {
            // Directly transfer ETH
            TransferHelper.safeTransferETH(to, bridgedAmount);
            emit WooCrossSwapOnDstChain(
                refId,
                msgSender,
                to,
                weth,
                bridgedAmount,
                toToken,
                ETH_PLACEHOLDER_ADDR,
                minToAmount,
                bridgedAmount,
                dst1inch.swapRouter == address(0) ? 0 : 1,
                0
            );
            return;
        }

        //some code...
    }
```

## Tool used

Manual Review

## Recommendation

Since `WooCrossChainRouterV4` contract lacks the functionality to save failed bridged transactions to be executed later; consider adding a mechanism to do so, and consider implementing a mechanism to enable owners of failed transactions to re-specify the receiver address of their bridged tokens in case of failure.
