Damp Wintergreen Seagull

high

# `WooracleV2_2.decimals()` function returns a hardcoded wrong price decimals

## Summary

`WooracleV2_2.decimals()` function returns a hardcoded wrong price decimals.

## Vulnerability Detail

- `WooracleV2_2.decimals()` function is called by the `WooPPV2.decimalInfo()` function when the amount of bought/sold assets are calculated, and to calculate the new price of the bought/sold base tokens:

  ```javascript
    function decimalInfo(address baseToken) public view returns (DecimalInfo memory) {
            return
                DecimalInfo({
                    priceDec: uint64(10) ** (IWooracleV2(wooracle).decimals(baseToken)), // 8
                    quoteDec: uint64(10) ** (IERC20Metadata(quoteToken).decimals()), // 18 or 6
                    baseDec: uint64(10) ** (IERC20Metadata(baseToken).decimals()) // 18 or 8
                });
        }
  ```

  and as can be noticed; `WooracleV2_2.decimals()` function assumes that the returned base token price is always in **8 decimals**:

  ```javascript
  function decimals(address) external pure override returns (uint8) {
          return 8;
      }
  ```

- By knowing that chainlink price feeds are going to be used to fetch the prices of the quote and base assets, and the `_cloPriceInQuote()` returns the price in **quote** decimals, where this returned price will be used later either to :

  - be used as a bound check for the manually saved base price
  - or using the returned chainlink price if the saved price is outdated and using chainlink price is preferred for that base token (when `!woFeasible && clo_preferred ` then use the `cloPrice`):

    ```javascript
        function _cloPriceInQuote(address _fromToken, address _toToken)
                internal
                view
                returns (uint256 refPrice, uint256 refTimestamp)
            {
                address baseOracle = clOracles[_fromToken].oracle;
                if (baseOracle == address(0)) {
                    return (0, 0);
                }
                address quoteOracle = clOracles[_toToken].oracle;
                uint8 quoteDecimal = clOracles[_toToken].decimal;

                (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
                (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();
                uint256 baseRefPrice = uint256(rawBaseRefPrice);
                uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

                // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
                uint256 ceoff = uint256(10)**quoteDecimal;
                refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
                refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
            }
    ```

**But where exactly does the returned decimals() doesn't match the actual price decimal?**
Let's look at the following example; knowing that the quote tokens are stable tokens such as USDC & USDT which have 6-decimals on some chains and 18 decimals on other chains (as in BNB smart chain):

1. The `WooracleV2_2` is deployed on BNB smart chain, where we have [USDC](https://bscscan.com/token/0x8AC76a51cc950d9822D68b83fE1Ad97B32Cd580d) as the quote token with 18-decimals, and [Binance-Peg Ethereum Token-ETH](https://bscscan.com/token/0x2170ed0880ac9a755fd29b2688956bd959f933f8) as a base token with 18-decimals.

2. In `_cloPriceInQuote()` function:

   - the `quoteOracle` is `USDC / USD` data feed with price returned in 8 decimals
   - and the `baseOracle` is `ETH / USD` data feed with price returned in 8 decimals

3. Now the `refPrice` will be returned in `quoteDecimal`; which is 18 decimals.

- So as can be noticed; if the `_cloPriceInQuote` returned price is consumed by the `WooPPV2` contract, then the price decimals are 18 not 8 decimals!

## Impact

- If we look at `WooPPV2._calcQuoteAmountSellBase()` function as an example, the calculated `gamma` will be larger than it's supposed to be (in our example it's larger by 1e10), and since this value is used to calculate the `quoteAmount` and the `newPrice`, then this will result in:

  ```javascript
  function _calcQuoteAmountSellBase(
          address baseToken,
          uint256 baseAmount,
          IWooracleV2.State memory state
      ) private view returns (uint256 quoteAmount, uint256 newPrice) {
      //...
          DecimalInfo memory decs = decimalInfo(baseToken);
      //...

            //! @audit-issue : (bAmount*e18) * (1e18) * coeff / (1e8 * 1e18) = bAmount*coeff*e10
            //! while it should be: (bAmount*e18) * (1e18) * coeff / (1e18 * 1e18) = bAmount*coeff
              gamma = (baseAmount * state.price * state.coeff) / decs.priceDec / decs.baseDec;
      //...
              quoteAmount =
                  (((baseAmount * state.price * decs.quoteDec) / decs.priceDec) *
                      (uint256(1e18) - gamma - state.spread)) /
                  1e18 /
                  decs.baseDec;
      //...
          // newPrice = oracle.price * (1 - k * oracle.price * baseAmount)
          newPrice = ((uint256(1e18) - gamma) * state.price) / 1e18;
      }
  ```

  - incorrect `quoteAmount` tokens, so the user will have incorrect amounts of quote tokens for his sold base:

  ```javascript
  quoteAmount =
    (((baseAmount * state.price * decs.quoteDec) / decs.priceDec) *
      (uint256(1e18) - gamma - state.spread)) /
    1e18 /
    decs.baseDec;
  ```

  - the `newPrice` of the bought base (that's sold by the user) will be reduced significantly:

  ```javascript
          // newPrice = oracle.price * (1 - k * oracle.price * baseAmount)
          newPrice = ((uint256(1e18) - gamma) * state.price) / 1e18;
      }
  ```

- Also if this is argued to be handled by increaing the `tokenInfos[baseToken].maxGamma` that the calculated gamma is checked against; this will not substitute as a solution as the `newPrice` of the base token will be decreased much higher than in other chains where USDC token has 6 decimals. This will result in base tokens prices having large decrepancies between chains.

## Code Snippet

[WooracleV2_2.decimals function](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L263C2-L265C6)

```javascript
  function decimals(address) external pure override returns (uint8) {
        return 8;
    }
```

## Tool used

Manual Review

## Recommendation

Update `WooracleV2_2.decimals()` function to return the correct price decimals; which is the decimals of the quote token.