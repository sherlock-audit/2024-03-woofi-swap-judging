Helpful Mustard Moose

medium

# 'WooracleV2_2::_cloPriceInQuote' missing positive price check before casting could lead to loss of funds

## Summary
User is able to execute 'WooRouterV2::swap' with unwanted token prices returned from Chainlink

## Vulnerability Detail
Here is one of many simple scenarios of what could happen:
1.  User calls 'WooRouterV2::swap' to swap some base tokens (whose price dropped below zero) for quote tokens
2. Then 'WooRouterV2' is going to transferFrom the negative-priced tokens and  call 'WooPPV2::swap', and then 'WooPPV2 _sellBase' will be executed to calculate and transfer the quoteAmount
```javascript
function swap(
        ...
    ) external override returns (uint256 realToAmount) {
       ...
        } else if (toToken == quoteToken) {
            // case 2: fromToken --> quoteToken
            realToAmount = _sellBase(fromToken, fromAmount, minToAmount, to, rebateTo);
        ...
    }
```
3. Then called 'WooPPV2::_sellBase' function will gain information about the quote amount from 
'WooPPV2::_calcQuoteAmountSellBase' executed with the current state of the baseToken, taken from 'WooracleV2_2::state'
```javascript
function _sellBase(
        ...
    ) private nonReentrant whenNotPaused returns (uint256 quoteAmount) {
        ...
        
        {
            uint256 newPrice;
            IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken); 
            (quoteAmount, newPrice) = _calcQuoteAmountSellBase(baseToken, baseAmount, state);
            IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
        }
       ...
    }
```
4. The returned state from 'WooracleV2_2::state' will contain wrong information about the price of the given baseToken due to bad info returned from  'WooracleV2_2::price' due to unsafe casting from int256 to uint256
```javascript
function state(address _base) external view override returns (State memory) {
        TokenInfo memory info = infos[_base];
        (uint256 basePrice, bool feasible) = price(_base);
        return State({price: uint128(basePrice), spread: info.spread, coeff: info.coeff, woFeasible: feasible});
    }

function price(address _base) public view override returns (uint256 priceOut, bool feasible) {
        uint256 woPrice_ = uint256(infos[_base].price);
        uint256 woPriceTimestamp = timestamp;

        (uint256 cloPrice_, ) = _cloPriceInQuote(_base, quoteToken);
        bool woFeasible = woPrice_ != 0 && block.timestamp <= (woPriceTimestamp + staleDuration);
        bool woPriceInBound = cloPrice_ == 0 ||
            ((cloPrice_ * (1e18 - bound)) / 1e18 <= woPrice_ && woPrice_ <= (cloPrice_ * (1e18 + bound)) / 1e18);

        if (woFeasible) {
            priceOut = woPrice_;
            feasible = woPriceInBound;
        } else {
            priceOut = clOracles[_base].cloPreferred ? cloPrice_ : 0;
            feasible = priceOut != 0;
        } // @audit if woFeasible if false and clOracles[_base].cloPreferred is true then veeeery dirty cloPrice_ number could be returned 
    }
```
5. Due to negative price and unsafe casting from int256 to uint256, the function is going to be EXECUTED WITH BASE TOKEN THAT HAS VERY BIG PRICE' instead of reverting due to negative price. (Note that the state of the base token must be woFeasible -> false and clOracles[_base].cloPreferred -> true)
6. Finally due to 'very big' baseTokenPrice, very big number of quote tokens will be transfered to the user


## Impact
Loss of funds

## Code Snippet
https://github.com/sherlock-audit/2024-03-woofi-swap/blob/main/WooPoolV2/contracts/wooracle/WooracleV2_2.sol#L348C5-L369C6

## Tool used

Manual Review

## Recommendation
Add positive price check as shown below:
```javascript
function _cloPriceInQuote(address _fromToken, address _toToken)
        internal
        view
        returns (uint256 refPrice, uint256 refTimestamp)
    {
        address baseOracle = clOracles[_fromToken].oracle;
        if (baseOracle == address(0)) {
            return (0, 0);
        }
        address quoteOracle = clOracles[_toToken].oracle;
        uint8 quoteDecimal = clOracles[_toToken].decimal;

        (, int256 rawBaseRefPrice, , uint256 baseUpdatedAt, ) = AggregatorV3Interface(baseOracle).latestRoundData();
        (, int256 rawQuoteRefPrice, , uint256 quoteUpdatedAt, ) = AggregatorV3Interface(quoteOracle).latestRoundData();

+++    require(rawBaseRefPrice > 0 && rawQuoteRefPrice > 0, 'Both rawRefPrices must be greater than zero!');

        uint256 baseRefPrice = uint256(rawBaseRefPrice);
        uint256 quoteRefPrice = uint256(rawQuoteRefPrice);

        // NOTE: Assume wooracle token decimal is same as chainlink token decimal.
        uint256 ceoff = uint256(10)**quoteDecimal;
        refPrice = (baseRefPrice * ceoff) / quoteRefPrice;
        refTimestamp = baseUpdatedAt >= quoteUpdatedAt ? quoteUpdatedAt : baseUpdatedAt;
    }
```
