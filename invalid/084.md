Damp Wintergreen Seagull

medium

# `WooCrossChainRouterV4.crossSwap()` doesn't revoke router allowance after swapping which would result in disabling using the allowed token again if the allowance is not fully consumed

## Summary

`WooCrossChainRouterV4.crossSwap()` doesn't revoke router allowance after swapping which would result in disabling using the allowed token again if the allowance is not fully consumed.

## Vulnerability Detail

- `WooCrossChainRouterV4.crossSwap()` function is meant to enable users from executing a cross-chain swap, where a cross chain swap transaction may include all or some of the following steps (as per the documentation):

> 1. Swap asset **A** in the user's wallet to asset **B** in WOOFi on the source chain
> 2. Then bridging asset **B** to asset **C** on the destination chain via Stargate (asset B and asset C are of the same value)
> 3. Then swap asset **C** to asset **D** in WOOFi on the destination chain and send to the wallet instructed by the user.

- So swapping from asset **A** to asset **B** on the source chain can be done either using a woofi pool (`WooPPV2`) via `wooRouter.swap()`, or it can be done via an external aggregater (where 1inch aggregator is going to be used) via ` wooRouter.externalSwap()` that redirects the swap call to the external aggregator:

  ```javascript
  // Step 2: local swap by 1inch router
  if (srcInfos.fromToken != srcInfos.bridgeToken) {
    TransferHelper.safeApprove(
      srcInfos.fromToken,
      address(wooRouter),
      srcInfos.fromAmount
    );
    if (src1inch.swapRouter != address(0)) {
      // external swap via 1inch
      bridgeAmount = wooRouter.externalSwap(
        src1inch.swapRouter,
        src1inch.swapRouter,
        srcInfos.fromToken,
        srcInfos.bridgeToken,
        srcInfos.fromAmount,
        srcInfos.minBridgeAmount,
        payable(address(this)),
        src1inch.data
      );
      fee = (bridgeAmount * srcExternalFeeRate) / FEE_BASE;
    } else {
      // swap via WOOFi
      bridgeAmount = wooRouter.swap(
        srcInfos.fromToken,
        srcInfos.bridgeToken,
        srcInfos.fromAmount,
        srcInfos.minBridgeAmount,
        payable(address(this)),
        to
      );
    }
  } else {
    //some code...
  }
  ```

- As can be noticed, the `wooRouter` is approved on the token amount that's going to be swapped via `TransferHelper.safeApprove()` from uniV3 lib, and after the swapping is done, the `bridgeAmount` will be returned, where it's checked to comply with the `srcInfos.minBridgeAmount` determined by the user.

## Impact

- Knowing that token **A** could be any token, USDT for example where it reverts if the allowance is reset from a non-zero value; then not clearing the allowance after the swapping operation if the current allowance is not fully consumed during the process (when `bridgeAmount < srcInfos.fromAmount`) will result in reverting any execution of the `crossSwap()` function with the same token being swapped (`srcInfos.fromToken`).

- Same issue when receiving the bridged tokens on `WooCrossChainRouterV4.sgReceive()`, where the received ERC20 is going to be swapped if the intended received token is not the same as the bridge token (from **C** to **D**); the allowance is not reset after the swapping operation success or failure (that's handled by `_handleERC20Received()`):

  ```javascript
  // Deduct the external swap fee
              uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
              bridgedAmount -= fee;

              TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
              if (dst1inch.swapRouter != address(0)) {
                  try
                      wooRouter.externalSwap(...);

                  returns (uint256 realToAmount) {
                      emit WooCrossSwapOnDstChain(... )


                  } catch {
                      bridgedAmount += fee;
                      TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                      emit WooCrossSwapOnDstChain(...);
                  }
              } else {
                  try wooRouter.swap(...)
                  returns (
                      uint256 realToAmount
                  ) {
                      emit WooCrossSwapOnDstChain(...);

                  } catch {
                      TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                      emit WooCrossSwapOnDstChain(...);

                  }
              }
  ```

## Code Snippet

[WooCrossChainRouterV4.crossSwap function/L96-L122](https://github.com/woonetwork/WooPoolV2/blob/a99e13de1492c17a325fff6cddb3696cd7db7dc9/contracts/CrossChain/WooCrossChainRouterV4.sol#L96C1-L122C18)

```javascript
            // Step 2: local swap by 1inch router
            if (srcInfos.fromToken != srcInfos.bridgeToken) {
                TransferHelper.safeApprove(srcInfos.fromToken, address(wooRouter), srcInfos.fromAmount);
                if (src1inch.swapRouter != address(0)) {
                    // external swap via 1inch
                    bridgeAmount = wooRouter.externalSwap(
                        src1inch.swapRouter,
                        src1inch.swapRouter,
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        src1inch.data
                    );
                    fee = (bridgeAmount * srcExternalFeeRate) / FEE_BASE;
                } else {
                    // swap via WOOFi
                    bridgeAmount = wooRouter.swap(
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        to
                    );
                }
```

[WooCrossChainRouterV4.\_handleERC20Received function/L412-L493](https://github.com/woonetwork/WooPoolV2/blob/a99e13de1492c17a325fff6cddb3696cd7db7dc9/contracts/CrossChain/WooCrossChainRouterV4.sol#L412-L493)

```javascript
// Deduct the external swap fee
            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            bridgedAmount -= fee;

            TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
            if (dst1inch.swapRouter != address(0)) {
                try
                    wooRouter.externalSwap(
                        dst1inch.swapRouter,
                        dst1inch.swapRouter,
                        bridgedToken,
                        toToken,
                        bridgedAmount,
                        minToAmount,
                        payable(to),
                        dst1inch.data
                    )
                returns (uint256 realToAmount) {
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        toToken,
                        minToAmount,
                        realToAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        fee
                    );
                } catch {
                    bridgedAmount += fee;
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        bridgedToken,
                        minToAmount,
                        bridgedAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                }
            } else {
                try wooRouter.swap(bridgedToken, toToken, bridgedAmount, minToAmount, payable(to), to) returns (
                    uint256 realToAmount
                ) {
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        toToken,
                        minToAmount,
                        realToAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                } catch {
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(
                        refId,
                        msgSender,
                        to,
                        bridgedToken,
                        bridgedAmount,
                        toToken,
                        bridgedToken,
                        minToAmount,
                        bridgedAmount,
                        dst1inch.swapRouter == address(0) ? 0 : 1,
                        0
                    );
                }
            }
```

## Tool used

Manual Review

## Recommendation

Clear swapped token allowance after each swapping operation:

```diff
    function crossSwap(
        uint256 refId,
        address payable to,
        SrcInfos memory srcInfos,
        DstInfos calldata dstInfos,
        Src1inch calldata src1inch,
        Dst1inch calldata dst1inch
    ) external payable whenNotPaused nonReentrant {

    //some code...

            // Step 2: local swap by 1inch router
            if (srcInfos.fromToken != srcInfos.bridgeToken) {
                TransferHelper.safeApprove(srcInfos.fromToken, address(wooRouter), srcInfos.fromAmount);
                if (src1inch.swapRouter != address(0)) {
                    // external swap via 1inch
                    bridgeAmount = wooRouter.externalSwap(
                        src1inch.swapRouter,
                        src1inch.swapRouter,
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        src1inch.data
                    );
+                   TransferHelper.safeApprove(srcInfos.fromToken, address(wooRouter),0);
                    fee = (bridgeAmount * srcExternalFeeRate) / FEE_BASE;
                } else {
                    // swap via WOOFi
                    bridgeAmount = wooRouter.swap(
                        srcInfos.fromToken,
                        srcInfos.bridgeToken,
                        srcInfos.fromAmount,
                        srcInfos.minBridgeAmount,
                        payable(address(this)),
                        to
                    );

+                   TransferHelper.safeApprove(srcInfos.fromToken, address(wooRouter),0);

                }

    //some code...

    }
```

```diff
    function _handleERC20Received(
        uint256 refId,
        address to,
        address toToken,
        address bridgedToken,
        uint256 bridgedAmount,
        uint256 minToAmount,
        Dst1inch memory dst1inch
    ) internal {
         //some code...

// Deduct the external swap fee
            uint256 fee = (bridgedAmount * dstExternalFeeRate) / FEE_BASE;
            bridgedAmount -= fee;

            TransferHelper.safeApprove(bridgedToken, address(wooRouter), bridgedAmount);
            if (dst1inch.swapRouter != address(0)) {
                try
                    wooRouter.externalSwap(...);
                returns (uint256 realToAmount) {
                    emit WooCrossSwapOnDstChain(... )


                } catch {
                    bridgedAmount += fee;
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(...);
                }

            } else {
                try wooRouter.swap(...)
                returns (
                    uint256 realToAmount
                ) {
                    emit WooCrossSwapOnDstChain(...);

                } catch {
                    TransferHelper.safeTransfer(bridgedToken, to, bridgedAmount);
                    emit WooCrossSwapOnDstChain(...);

                }


            }

+                TransferHelper.safeApprove(bridgedToken, address(wooRouter), 0);

}
```