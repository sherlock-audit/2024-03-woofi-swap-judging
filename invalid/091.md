Damp Wintergreen Seagull

medium

# `WooPPV2._sellBase()` function updates the base token price before deducting the fees which will result in a lower updated price for that base token

## Summary

`WooPPV2._sellBase()` function updates the base token price before deducting the fees which will result in a lower updated price for that base token.

## Vulnerability Detail

- `WooPPV2` contract deductes a fee amount from each swap in quote token, for example: when a user sells his quote tokens to get base tokens, the fee amount is calculated as follows:

  ```javascript
  function _sellQuote(
          address baseToken,
          uint256 quoteAmount,
          uint256 minBaseAmount,
          address to,
          address rebateTo
      ) private nonReentrant whenNotPaused returns (uint256 baseAmount) {
          //some code

          uint256 swapFee = (quoteAmount * tokenInfos[baseToken].feeRate) / 1e5;
          quoteAmount = quoteAmount - swapFee;
          unclaimedFee = unclaimedFee + swapFee;

          {
              uint256 newPrice;
              IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken);
              (baseAmount, newPrice) = _calcBaseAmountSellQuote(baseToken, quoteAmount, state);


              IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
              // console.log('Post new price:', newPrice, newPrice/1e8);
              require(baseAmount >= minBaseAmount, "WooPPV2: baseAmount_LT_minBaseAmount");
          }

          //some code...
      }
  ```

- As can be seen, the equivalent bought base token amount is calculated based on the `quoteAmount` after fee deduction, where `_calcBaseAmountSellQuote()` will return the base amount that is going to be sent to the user and the new price of the bought base tokens.

- The updated base price depends on its reserve:

  - When users buy this base token (by selling their quote tokens), then the reserves of this token will be **decreased** its price will **increase**.
  - And when users sell this base token for quote tokens, then the reserves of this base token will be **increased** and its price will **decrease**.
  - So the more the sold `quoteAmount`; the less the reserves of the bought base token, and the higher its new price will be.

  ```javascript
  function _calcBaseAmountSellQuote(
          address baseToken,
          uint256 quoteAmount,
          IWooracleV2.State memory state
      ) private view returns (uint256 baseAmount, uint256 newPrice) {
          require(state.woFeasible, "WooPPV2: !ORACLE_FEASIBLE");

          DecimalInfo memory decs = decimalInfo(baseToken);

          // gamma = k * quote_amount; and decimal 18
          uint256 gamma;
          {
              require(quoteAmount <= tokenInfos[baseToken].maxNotionalSwap, "WooPPV2: !maxNotionalValue");

              gamma = (quoteAmount * state.coeff) / decs.quoteDec;
              require(gamma <= tokenInfos[baseToken].maxGamma, "WooPPV2: !gamma");

              // Formula: baseAmount = quoteAmount / oracle.price * (1 - oracle.k * quoteAmount - oracle.spread)
              baseAmount =
                  (((quoteAmount * decs.baseDec * decs.priceDec) / state.price) *
                      (uint256(1e18) - gamma - state.spread)) /
                  1e18 /
                  decs.quoteDec;
          }

          // new_price = oracle.price / (1 - k * quoteAmount)
          newPrice = (uint256(1e18) * state.price) / (uint256(1e18) - gamma);
      }
  ```

- Now let's see how `_sellBase()` function works:

  - it will first calculate the `quoteAmount` and the `newPrice` of the bought base token,
  - then the fee will be deducted from the returned `quoteAmount`.

  ```javascript
  function _sellBase(
          address baseToken,
          uint256 baseAmount,
          uint256 minQuoteAmount,
          address to,
          address rebateTo
      ) private nonReentrant whenNotPaused returns (uint256 quoteAmount) {
          //some code...

  {
              uint256 newPrice;
              IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken);
              (quoteAmount, newPrice) = _calcQuoteAmountSellBase(baseToken, baseAmount, state);
              IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
              // console.log('Post new price:', newPrice, newPrice/1e8);
          }

          uint256 swapFee = (quoteAmount * tokenInfos[baseToken].feeRate) / 1e5;
          quoteAmount = quoteAmount - swapFee;
          require(quoteAmount >= minQuoteAmount, "WooPPV2: quoteAmount_LT_minQuoteAmount");

          unclaimedFee = unclaimedFee + swapFee;

          //some code..
      }
  ```

## Impact

- But as can be noticed, the full `baseAmount` is used to calculate the `quoteAmount` **before deducting an equivalent fee from the traded base amount**, and since the price of the base token is influenced by its reserves, then invoking `_calcQuoteAmountSellBase()` with higher `baseAmount` (as it's used before deducting an equivalent fee) results in a higher calculated `gamma` --> which leds to a lower `newPrice` of the base token!

  ```javascript
      function _calcQuoteAmountSellBase(
          address baseToken,
          uint256 baseAmount,
          IWooracleV2.State memory state
      ) private view returns (uint256 quoteAmount, uint256 newPrice) {
          //some code...

              gamma = (baseAmount * state.price * state.coeff) / decs.priceDec / decs.baseDec;
          //some code...
              quoteAmount =
                  (((baseAmount * state.price * decs.quoteDec) / decs.priceDec) *
                      (uint256(1e18) - gamma - state.spread)) /
                  1e18 /
                  decs.baseDec;
          }

          // newPrice = oracle.price * (1 - k * oracle.price * baseAmount)
          newPrice = ((uint256(1e18) - gamma) * state.price) / 1e18;
      }
  ```

## Code Snippet

[WooPPV2.\_sellBase function](https://github.com/sherlock-audit/2024-03-woofi-swap/blob/65185691c91541e33f84b77d4c6290182f137092/WooPoolV2/contracts/WooPPV2.sol#L420C1-L465C6)

```javascript
    function _sellBase(
        address baseToken,
        uint256 baseAmount,
        uint256 minQuoteAmount,
        address to,
        address rebateTo
    ) private nonReentrant whenNotPaused returns (uint256 quoteAmount) {
        require(baseToken != address(0), "WooPPV2: !baseToken");
        require(to != address(0), "WooPPV2: !to");
        require(baseToken != quoteToken, "WooPPV2: baseToken==quoteToken");

        require(balance(baseToken) - tokenInfos[baseToken].reserve >= baseAmount, "WooPPV2: !BASE");

        {
            uint256 newPrice;
            IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken);
            (quoteAmount, newPrice) = _calcQuoteAmountSellBase(baseToken, baseAmount, state);
            IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
            // console.log('Post new price:', newPrice, newPrice/1e8);
        }

        uint256 swapFee = (quoteAmount * tokenInfos[baseToken].feeRate) / 1e5;
        quoteAmount = quoteAmount - swapFee;
        require(quoteAmount >= minQuoteAmount, "WooPPV2: quoteAmount_LT_minQuoteAmount");

        unclaimedFee = unclaimedFee + swapFee;

        tokenInfos[baseToken].reserve = uint192(tokenInfos[baseToken].reserve + baseAmount);
        tokenInfos[quoteToken].reserve = uint192(tokenInfos[quoteToken].reserve - quoteAmount - swapFee);

        if (to != address(this)) {
            TransferHelper.safeTransfer(quoteToken, to, quoteAmount);
        }

        emit WooSwap(...);
    }
```

## Tool used

Manual Review

## Recommendation

One suggestion could be updating `WooPPV2._sellBase()` function to calculate and deduct the equivalet amount of fees in quote token (that's calculated based on the base tokens amount) before calling `_calcQuoteAmountSellBase()`:

```javascript
    function _sellBase(
        address baseToken,
        uint256 baseAmount,
        uint256 minQuoteAmount,
        address to,
        address rebateTo
    ) private nonReentrant whenNotPaused returns (uint256 quoteAmount) {
        require(baseToken != address(0), "WooPPV2: !baseToken");
        require(to != address(0), "WooPPV2: !to");
        require(baseToken != quoteToken, "WooPPV2: baseToken==quoteToken");

        require(balance(baseToken) - tokenInfos[baseToken].reserve >= baseAmount, "WooPPV2: !BASE");

        {
            uint256 newPrice;
            IWooracleV2.State memory state = IWooracleV2(wooracle).state(baseToken);

+            uint256 quoteAmountEQ =
                ((baseAmount * state.price * decs.quoteDec) / decs.priceDec) / decs.baseDec;

+            uint256 swapFee = (quoteAmountEQ * tokenInfos[baseToken].feeRate) / 1e5;
+           quoteAmountEQ = quoteAmountEQ - swapFee;
+            baseAmount =
                ((quoteAmountEQ * decs.baseDec * decs.priceDec) / state.price) /decs.quoteDec;

            (quoteAmount, newPrice) = _calcQuoteAmountSellBase(baseToken, baseAmount, state);
            IWooracleV2(wooracle).postPrice(baseToken, uint128(newPrice));
            // console.log('Post new price:', newPrice, newPrice/1e8);
        }

-       uint256 swapFee = (quoteAmount * tokenInfos[baseToken].feeRate) / 1e5;
-       quoteAmount = quoteAmount - swapFee;
        require(quoteAmount >= minQuoteAmount, "WooPPV2: quoteAmount_LT_minQuoteAmount");

        unclaimedFee = unclaimedFee + swapFee;

        tokenInfos[baseToken].reserve = uint192(tokenInfos[baseToken].reserve + baseAmount);
        tokenInfos[quoteToken].reserve = uint192(tokenInfos[quoteToken].reserve - quoteAmount - swapFee);

        if (to != address(this)) {
            TransferHelper.safeTransfer(quoteToken, to, quoteAmount);
        }

        emit WooSwap(...);
    }
```
